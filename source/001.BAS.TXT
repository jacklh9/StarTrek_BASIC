10 '001.BAS/GAME -- STAR TREK:TNG the GAME -- LAST MODIFIED: 12-11-95
20 KEY OFF:KEY 15,CHR$(4)+CHR$(70):ON KEY(15)GOSUB 11320:KEY(15)ON
30 TIMER OFF:RANDOMIZE TIMER:TIMER ON:ON ERROR GOTO 10880
40 IF LEN(Z$)<>25 THEN RUN"002" ELSE IF HP<>0 THEN GOSUB 1560
50 IF HP=0 THEN 100 ELSE IF K3>0 THEN ON TIMER(30) GOSUB 10610:TIMER ON
60 IF ST>DL THEN 4210 ELSE IF K9<=0 THEN 4290
70 GOSUB 260:ON HP GOSUB 6890,7690,7650,7670,7820,7820,8810
80 IF HP=8 THEN IF HP(1)=1 AND ST=T0 THEN HP(1)=0:GOSUB 260:GOTO 1140
90 ON HP-7 GOTO 1720,1720,1720
100 GOSUB 120:GOTO 530
110 REM MAPPING
120 MG$=CHR$(196)+CHR$(197)+CHR$(196)
130 IF D(2)<0 THEN RETURN
140 MAP$=MG$+MG$+MG$+MG$+MG$+MG$+MG$+MG$
150 FOR LP=1 TO 4:LP.1=LP+4:LOCATE LP+15,4:PRINT MAP$
160 IF LP.1=8 THEN LOCATE 23,4:MP1$=CHR$(196)+CHR$(193)+CHR$(196):FOR LP1=1 TO 8:PRINT MP1$;:NEXT LP1:GOTO 180
170 LOCATE LP.1+15,4:PRINT MAP$
180 NEXT LP:RETURN
190 REM COMBADGE
200 PUT (232,104),CM,PSET:SOUND 32767,0:RETURN
210 REM CLEAR & PAUSE
220 SOUND 100,2:TM=TIMER
230 WHILE INKEY$="" AND TIMER<=TM+15:WEND
240 PUT (232,104),CM,PSET:SOUND INT(RND(1)*2000+5000),1
250 REM ERASE PANEL
260 LINE (232,104)-(631,183),0,BF:LINE (276,74)-(355,76),0,BF:LOCATE 14,3:PRINT SPACE$(26)
270 IF C$="R" OR C$="Y" OR C$="D" AND K3>0 THEN LINE (16,32)-(23,55),0,BF:LINE (600,32)-(607,55),0,BF
280 ANY$=INKEY$:IF ANY$<>"" THEN SOUND INT(RND*2000+100),1:GOTO 280 'CLR BUFFER
290 RETURN
300 REM CLS VIEWER
310 LINE (85,8)-(536,70),0,BF:RETURN
320 REM TIMER PAUSE
330 TM=TIMER:IF TD=0 THEN TD=3.5
340 WHILE INKEY$="" AND TIMER<=TM+TD:WEND
350 TD=0:TM=0:GOTO 240
360 REM HULL BREACH
370 IF C$="D" THEN E=E0:S=0:GOTO 1720 ELSE TIMER OFF
380 FOR LP=1 TO 10:KEY(LP)OFF:NEXT LP:KEY OFF:GOSUB 260:PLAY"MF"
390 FOR LP=1 TO 13:ON LP GOSUB 420,430,440,450,460,470,480,430,440,450,460,470,480
400 SOUND 1000,1:SOUND 32767,1:SOUND 1000,9:SOUND 32767,10
410 IF TM+2>TIMER THEN 410 ELSE NEXT LP:GOTO 490
420 GOSUB 10490:LOCATE 15,35:PRINT"COMPUTER:±EMERGENCY±ALERT":RETURN
430 LOCATE 20,49:PRINT"SHIELDS COLLAPSED     ":RETURN
440 LOCATE 21,49:PRINT"HULL BREACH DECK ";USING"##_   ";INT(RND*25+10):RETURN
450 LOCATE 20,49:PRINT"MAIN SYSTEMS FAILURE  ":RETURN
460 LOCATE 21,49:PRINT"LIFE SUPPORT FAILING  ":RETURN
470 LOCATE 20,49:PRINT"WARP CORE BREACH IMMI-":RETURN
480 LOCATE 21,49:PRINT"NENT - EVACUATE SHIP *":RETURN
490 X$=SPACE$(22):LOCATE 20,49:PRINT X$:LOCATE 21,49:PRINT X$:LOCATE 23,30:PRINT"[COUNSELOR TROI: The crew's in a panic, Captain!!]":TM=TIMER
500 IF TM+5>TIMER THEN 500 ELSE LINE (0,0)-(639,199),1,BF:SOUND 1000,50:TM=TIMER
510 IF TM+3<TIMER THEN 520 ELSE 510
520 FOR LP=1 TO 300:SOUND INT(RND(1)*1000+37),.1:NEXT LP:GOTO 4240
530 REM DEFs
540 GOSUB 300:GOSUB 10980
550 DIM CM(1000):GET (232,104)-(631,183),CM
560 DEF FNPOW(SE)=INT(SE/200):DEF FNE(E,VEL,D)=INT(E-INT(10*(VEL*D)))
570 DEF FND(D)=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
580 DEF FNR(R)=INT(RND(R)*7.98+1.01):GOTO 1070
590 REM STARBASES
600 IF K3>0 THEN RETURN ELSE IF C$="D" THEN 680 ELSE IF B3<=0 THEN RETURN
610 IF Z<>1 THEN 660
620 IF STB>100 THEN GFX$="00B":X(1)=280:X(2)=304:IF C$<>"D" AND K3<=0 THEN RETURN ELSE 710
630 IF STB<100 THEN IF STB/2=INT(STB/2) THEN GFX$="00H":X(1)=240:X(2)=360 ELSE GFX$="00F":X(1)=256:X(2)=448
640 IF STB=100 THEN GFX$="00C":X(1)=256:X(2)=320:IF C$<>"D" AND K3<=0 THEN RETURN ELSE 710
650 IF C$="D" AND K3<=0 THEN RETURN ELSE 710
660 IF STB>100 THEN GFX$="00B" ELSE IF STB<100 THEN GFX$="00D" ELSE GFX$="00C"
670 GOTO 710
680 REM DOCKED
690 IF STB>100 THEN GFX$="00E" ELSE IF STB<100 THEN IF STB/2=INT(STB/2) THEN GFX$="00H" ELSE GFX$="00F"
700 IF STB=100 THEN GFX$="00G"
710 GOSUB 11040:RETURN
720 REM STB NEAR
730 GOSUB 250:IF STB=0 THEN STB=INT(100*(Q2/Q1)):GOSUB 260 ELSE GOSUB 4400:GOTO 770
740 LOCATE 14,30:PRINT D$;"Captain, sensors show starbase";STB
750 LOCATE 15,30:PRINT"is in proximity.":IF K3<=0 THEN GOSUB 590
760 SB.SHE=INT(RND(1)*3+2):GOSUB 210:RETURN
770 IF C$="D" THEN LOCATE 15,30:PRINT D$;"Captain, we are still docked.":GOTO 810
780 LOCATE 14,30:PRINT D$;"Captain, we have successfully"
790 LOCATE 15,30:PRINT"docked with starbase";STB;".":C$="D"
800 IF K3<=0  THEN GOSUB 590
810 FOR I=1 TO 8:IF D(I)>=0 THEN D(I)=0
820 NEXT I:RETURN
830 REM ENEMY NEAR
840 GOSUB 260:X$="":X1$="a":IF K3>1 THEN X$="s"
850 IF K3=2 THEN X1$="two" ELSE IF K3=3 THEN X1$="three"
860 LOCATE 14,30:PRINT W$;"Sensors have picked up ";X1$
870 IF X2$="X" THEN X1$=" decloaking" ELSE X1$="":X2$=""
880 LOCATE 15,30:PRINT"Romulan warbird";X$;X1$;" in the quadrant."
890 GFX$="00I":GOSUB 11040:TM=TIMER
900 ANY$=INKEY$:IF ANY$="" AND TIMER<=TM+1 THEN 900
910 LOCATE 17,30:IF C$="D" THEN PRINT R$;"Battle stations! Notify":LOCATE 18,30:PRINT"starbase";STB;"of their presence, Lieutenant.":RETURN
920 PRINT R$;"Red Alert!!!":C$="R":RETURN
930 REM CMPTR STAT
940 IF D(8)>=0 THEN Z3=0:RETURN
950 IF ANY$=CHR$(50) THEN IF SHD>=10 THEN SHD=SHD-10:GOSUB 7550 ELSE SHD=0:GOSUB 7550
960 GOSUB 10490:LOCATE 15,35:PRINT"CONTROL PANEL STATUS:"
970 LOCATE 20,49:PRINT"MAIN COMPUTER"
980 LOCATE 21,49:PRINT"FUNCTIONS OFF-LINE"
990 GOSUB 210:Z3=1:RETURN
1000 REM CHAIN CMP FILES
1010 IF HP=0 THEN 5740 ELSE KEY OFF
1020 GOSUB 10490:LOCATE 14,3:PRINT" ÕÕ LIBRARY COMPUTER ÕÕ":LOCATE 15,35:PRINT"REFERENCE±FILES:"
1030 IF D(8)<0 THEN LOCATE 20,49:PRINT"INTERFACE DAMAGED    "
1040 IF D(8)<0 THEN LOCATE 21,49:PRINT"REROUTING INTERFACE":GOTO 1060
1050 LOCATE 21,49:PRINT"ACCESSING  FILES"
1060 FOR LP=1 TO 10:KEY(LP)OFF:KEY LP,"±±±±±±":NEXT LP:CHAIN "003",,ALL:RETURN
1070 REM NEW QUADRANT
1080 Z4=Q1:Z5=Q2:K3=0:B3=0:S3=0:G5=0:D4=.5*RND(1):Z(Q1,Q2)=G(Q1,Q2)
1090 TIMER OFF:KEY OFF:FOR LP=1 TO 10:KEY(LP)OFF:NEXT LP:C$="G"
1100 IF Q1<1 OR Q1>8 OR Q2<1 OR Q2>8 THEN 1410 ELSE O.REG$=REG$
1110 GOSUB 5490:REG$=G2$:LOCATE 14,3:PRINT SPACE$(26):GOSUB 260
1120 IF X3$<>"NEW" THEN 1170 'NEW ORDERS
1130 X3$="":HP=8:HP(1)=1:CHAIN"003",,ALL
1140 W1=4:VEL=9:GOSUB 120:GOSUB 10930:LOCATE 14,30:PRINT R$;"Our new mission begins in the":E=4000:S=6000
1150 LOCATE 15,30:PRINT G2$;" quadrant.":GOSUB 320:GOTO 1270
1160 GOSUB 320:SOUND 32767,0:IF X5=1 AND REG$<>O.REG$ THEN GOSUB 2520:GOTO 1270 ELSE 1270
1170 IF W1<1 AND W1>0 THEN GOSUB 260:LOCATE 17,30:PRINT WC$;"Engaging impulse engines":GOTO 1190
1180 GOSUB 260:LOCATE 17,30:PRINT WC$;"Course laid in. Engaging warp";VEL
1190 LOCATE 18,30:PRINT "for ";REG$;" on your mark, Sir.":TD=90:GOSUB 320:IF VEL<=0 THEN VEL=1
1200 GOSUB 10490:LOCATE 15,35:IF W1<1 AND W1>0 THEN PRINT"IMPULSE±ENGINES±ENGAGED" ELSE PRINT "WARP±FACTOR:±";USING"#";VEL
1210 LOCATE 17,47:PRINT"DESTINATION:±";G2$
1220 LOCATE 21,55:PRINT"ENGAGE....":IF W1<1 AND W1>0 THEN GOSUB 120:GOSUB 300:SOUND 100,500*W1+100:GOSUB 11190:GOSUB 10980:GOTO 1240
1230 GOSUB 120:GOSUB 10930
1240 ST=ST+W1/VEL:E=FNE(E,VEL,W1)
1250 GOSUB 260:LOCATE 17,30:PRINT D$;"Now entering ":LOCATE 18,30:PRINT "quadrant ";G2$;".":GOTO 1160
1260 REM Quad
1270 K3=INT(G(Q1,Q2)*.01):B3=INT(G(Q1,Q2)*.1)-10*K3
1280 S3=G(Q1,Q2)-100*K3-10*B3
1290 IF ST<>T0 THEN GOSUB 2070
1300 GOSUB 260:GOTO 1380
1310 REM Red Alert
1320 GOSUB 830:SOUND 32726,0
1330 FOR LP=1 TO 3:SND=137:SOUND SND,5:FOR SND1=SND TO SND+750 STEP 6:SOUND SND1,.2:NEXT SND1:SOUND 32767,5:NEXT LP
1340 X=INT(RND(1)*10+20):ON TIMER(X) GOSUB 10610:TIMER ON
1350 PANEL$=CHR$(176):GOSUB 6300
1360 IF S>=2000 OR C$="D" THEN GOSUB 260:RETURN
1370 SOUND 100,2:SOUND 150,9:LOCATE 21,30:PRINT W$;"Shields extremely low, Captain.":GOSUB 320:RETURN
1380 GOSUB 10490:LOCATE 15,35:PRINT"SHORT±RANGE±SENSORS":IF D(2)>=0 THEN LOCATE 21,54:PRINT"Õ- SCANNING -Õ" ELSE LOCATE 21,49:PRINT"SENSORS INOPERATIVE"
1390 IF B3>0 THEN STB=0:SOUND 1000,1:GOSUB 720
1400 FOR I=1 TO 3:K(I,1)=0:K(I,2)=0:NEXT I
1410 FOR I=1 TO 3:K(I,3)=0:NEXT I:Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
1420 FOR LP=1 TO 8:MAP$(LP)=MAP$:NEXT LP
1430 REM  POS. =-@ , *-]
1440 REM  <+> ,& *
1450 A$="=-@":Z1=S1:Z2=S2:GOSUB 5290;IF K3<1 THEN 1480
1460 FOR I=1 TO K3:GOSUB 5260:A$="*-]":Z1=R1:Z2=R2
1470 GOSUB 5290:K(I,1)=R1:K(I,2)=R2:K(I,3)=INT(S9*45+1000*RND(1)):NEXT I
1480 IF B3<1 THEN 1500
1490 GOSUB 5260:A$="<+>":Z1=R1:B4=R1:Z2=R2:B5=R2:GOSUB 5290
1500 FOR I=1 TO S3:GOSUB 5260:IF S3<3 THEN IF I=1 THEN A$=" "+CHR$(15)+" " ELSE A$=" * "
1510 IF S3>=3 THEN IF I=INT(S3/2) THEN A$=" "+CHR$(15)+" " ELSE A$=" * "
1520 Z1=R1:Z2=R2:GOSUB 5290:NEXT I
1530 IF K3>0 THEN SOUND 1000,1:GOSUB 1310:GOSUB 10490:LOCATE 15,35:PRINT"SHORT±RANGE±SENSORS":LOCATE 21,54:IF D(2)>=0 THEN PRINT"Õ- SCANNING -Õ" ELSE PRINT"˛˛˛˛˛˛˛˛˛˛˛"
1540 GOSUB 4300:GOSUB 260:GOSUB 3660:GOSUB 1560:GOTO 1600
1550 REM
1560 IF STD>DL THEN 4210
1570 IF K9<=0 THEN 4290
1580 IF K3<=0 THEN TIME(1)=0:TIMER STOP:ON TIMER(INT(RND(1)*40+40))GOSUB 9660:TIMER ON:RETURN
1590 REM FUEL CHECK
1600 IF S+E<=0 THEN 1620
1610 IF S+E>0 THEN IF S>E AND E=0 AND D(7)<-1 AND D(6)<0 THEN ELSE 1720
1620 IF C$="D" THEN E=E0:S=0:GOTO 1720
1630 GOSUB 260:LOCATE 14,30:PRINT"COMPUTER: INSUFFICIENT ENERGY AVAILABLE.":SOUND 400,1:SOUND 32767,5:SOUND 400,15
1640 TIMER OFF:FOR LP=1 TO 10:KEY(LP)OFF:KEY LP,"":NEXT LP:KEY OFF
1650 LOCATE 15,30:PRINT"SHIELD CONTROL IS PRESENTLY INCAPABLE OF"
1660 LOCATE 16,30:PRINT"CROSS-CIRCUITING TO RESERVE INTERFACE."
1670 LOCATE 17,30:PRINT"INCAPABLE OF COMPLETING MANEUVERS. ALL VITAL":LOCATE 18,30:PRINT"SYSTEMS ON EMERGENCY POWER."
1680 IF S+E>0 THEN IF D(7)>-1 OR D(6)>=0 THEN S=S+E:E=0:TD=90:GOSUB 320:GOTO 1720
1690 LOCATE 21,30:PRINT"EMERGENCY DISTRESS SIGNAL BEING SENT OUT... ":TD=60:GOSUB 320
1700 LOCATE 15,30:PRINT L$;"Sir, our dilithium crystals are":LOCATE 16,30:PRINT"exhausted, completely. We aren't going anywhere.":LOCATE 17,30:PRINT"We have only enough for life-support.":LOCATE 19,30
1710 PRINT R$;"We were unable to fulfill our duty.":LOCATE 20,30:PRINT"We must abort the mission....":TD=60:GOSUB 320:GOTO 4220
1720 REM Com Menu
1730 GOSUB 5740:GOTO 6220
1740 REM COURSE CONTROL
1750 REM NO WARP
1760 IF D(1)>-1 THEN RETURN
1770 LOCATE 15,30:PRINT"They won't be back on-line for a while. Until"
1780 LOCATE 16,30:PRINT"then we'll have minimal travel capabilities."
1790 LOCATE 17,30:PRINT"With only impulse engines, travel will be time"
1800 LOCATE 18,30:PRINT"consuming. If there's a starbase in proximity"
1810 LOCATE 19,30:PRINT"I suggest we make use of it, Captain.    ---- "
1820 TD=60:RETURN
1830 IF E+S<=0 THEN FOR LP=1 TO 10:KEY(LP)OFF:NEXT LP
1840 IF E+S>=1000 THEN RETURN ELSE GOSUB 260:LOCATE 14,30:PRINT L$;"Captain,";:IF E+S<500 THEN SOUND 800,1:SOUND 32767,1:SOUND 800,8
1850 PRINT"the ships dilithium":LOCATE 15,30:PRINT"crystals are nearly exhausted!"
1860 LOCATE 16,30:PRINT"We need to get to a starbase immediately."
1870 LOCATE 17,30:PRINT"For now, the distance, coordinates and warp"
1880 LOCATE 18,30:PRINT"factors must be reconfigured to work with"
1890 LOCATE 19,30:PRINT"the remaining energy. We're risking being "
1900 LOCATE 20,30:PRINT"stranded in space. May I remind the captain that"
1910 LOCATE 21,30:PRINT"high warp velocities consume vast energy.":GOSUB 210
1920 IF S<E THEN RETURN ELSE GOSUB 260:IF S+E<=0 THEN 1590
1930 LOCATE 14,30:PRINT WC$;"Sir, deflector room acknowledges"
1940 LOCATE 15,30:PRINT USING"###_% of remaining energy presently";INT((S/(E+S))*100)-1:LOCATE 16,30:PRINT "deployed to shields. Our reserve energy"
1950 LOCATE 17,30:PRINT"supply is extremely low, Captain."
1960 SOUND 100,5:SOUND 37,9
1970 GOSUB 210:RETURN
1980 REM Travel
1990 IF W1>=1 THEN N=INT(W1*8) ELSE N=INT(W1*10)
2000 KEY OFF:FOR LP=1 TO 10:KEY LP,"":KEY (LP)OFF:NEXT LP
2010 REM ROMs MOVE/FIRE ON MOVING SHIP
2020 FOR I=1 TO 3:IF K(I,3)<=0 THEN 2050
2030 A$="   ":Z1=K(I,1):Z2=K(I,2):GOSUB 5290:GOSUB 5260
2040 K(I,1)=Z1:K(I,2)=Z2:A$="*-]":GOSUB 5290
2050 NEXT I:GOSUB 3940:D1=0:D6=W1:IF W1>=1 THEN D6=1
2060 GOTO 2220
2070 REM DAMAGE & REPAIR REPORTS
2080 FOR I=1 TO 8:IF D(I)>=0 THEN 2140
2090 D(I)=D(I)+D6:IF D(I)>-.1 AND D(I)<0 THEN D(I)=-.1:GOTO 2140
2100 IF D(I)<0 THEN 2140
2110 IF D1<>1 THEN D1=1:GOSUB 260:LOCATE 14,30:PRINT L$;
2120 R1=I:GOSUB 5340:LOCATE 15,30:PRINT G2$;" repair completed."
2130 IF INT(I)=2 THEN GOSUB 4400 ELSE GOSUB 210
2140 NEXT I:IF RND(1)>.2 THEN 2210
2150 R1=FNR(1):IF RND(1)>=.6 THEN 2190
2160 D(R1)=D(R1)-(RND(1)*5+1)
2170 GOSUB 5340:GOSUB 260:LOCATE 14,30:PRINT L$;G2$;" damaged.":IF INT(R1)=2 THEN GOSUB 4400 ELSE IF INT(R1)=1 AND D(1)<0 THEN GOSUB 1750
2180 GOSUB 320:GOTO 2210
2190 D(R1)=D(R1)+RND(1)*3+1
2200 GOSUB 5340:GOSUB 260:LOCATE 14,30:PRINT L$;G2$:LOCATE 15,30:PRINT"state of repair improved.":GOSUB 210
2210 RETURN
2220 REM MOVE SHIP
2230 A$="   ":Z1=INT(S1):Z2=INT(S2):GOSUB 5290
2240 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):X=S1:Y=S2
2250 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):Q4=Q1:Q5=Q2
2260 FOR I=1 TO N:S1=S1+X1:S2=S2+X2:IF S1<1 OR S1>=9 OR S2<1 OR S2>=9 THEN 2420
2270 S8=INT(S1)*24+INT(S2)*3-26:IF MID$(Q$,S8,2)="  " THEN 2350
2280 S1=INT(S1-X1):S2=INT(S2-X2):GOSUB 260:ANY$=MID$(Q$,S8,3)
2290 A$="=-@":Z1=INT(S1):Z2=INT(S2):GOSUB 5290:GOSUB 4400
2300 LOCATE 14,30:PRINT D$;"Sir, we cannot navigate the"
2310 IF ANY$="<+>" THEN ANY$=" the starbase" ELSE IF ANY$=" "+CHR$(15)+" " THEN ANY$=" a star" ELSE IF ANY$=" * " THEN ANY$=" a planet" ELSE IF ANY$="*-]" THEN ANY$=" another vessel"
2320 LOCATE 15,30:PRINT"Enterprise through";ANY$;"."
2330 LOCATE 16,30:PRINT"Warp engines disengaged at sector (";USING"#_,#_).";S2,S1
2340 GOSUB 210:GOTO 2360
2350 NEXT I:S1=INT(S1):S2=INT(S2)
2360 A$="=-@":Z1=INT(S1):Z2=INT(S2):GOSUB 5290:GOSUB 2620:T8=1
2370 IF W1<1 THEN T8=.1*INT(10*W1)
2380 IF ST>DL THEN 4220
2390 REM DOCKED?
2400 GOSUB 4300:GOSUB 260:GOSUB 1560:GOTO 1600
2410 REM EXCEED BORDER
2420 X=8*Q1+X+N*X1:Y=8*Q2+Y+N*X2:Q1=INT(X/8):Q2=INT(Y/8):S1=INT(X-Q1*8)
2430 S2=INT(Y-Q2*8):IF S1=0 THEN Q1=Q1-1:S1=8
2440 IF S2=0 THEN Q2=Q2-1:S2=8
2450 X5=0:IF Q1<1 THEN X5=1:Q1=1:S1=1
2460 IF Q1>8 THEN X5=1:Q1=8:S1=8
2470 IF Q2<1 THEN X5=1:Q2=1:S2=1
2480 IF Q2>8 THEN X5=1:Q2=8:S2=8
2490 IF X5=0 THEN 2590 ELSE ST=ST+.1
2500 REM ATTEMPTED CROSSING
2510 LOCATE 15,30:PRINT D$;"Coordinates exceed Federation":LOCATE 16,30:PRINT"Space limits.":GOSUB 320:GOTO 2580
2520 GOSUB 260:LOCATE 14,30:PRINT R$;"You attempted to cross over"
2530 LOCATE 15,30:PRINT"Federation Space & beyond the galactic perimeter."
2540 LOCATE 16,30:PRINT"Sir, Starfleet did not authorized such an action."
2550 LOCATE 18,30:PRINT D$;"Enterprise halted in quadrant "
2560 LOCATE 19,30:PRINT REG$;", sector (";USING"#";S2;:PRINT USING"_,#_).";S1
2570 GOSUB 210:PERI.REG$="":RETURN
2580 IF ST>T0+T9 THEN 4220
2590 IF 8*Q1+Q2=8*Q4+Q5 THEN 2360
2600 ST=ST+1:GOSUB 2620:GOTO 1080
2610 REM Maneuver Energy
2620 IF E>=0 THEN RETURN ELSE IF D(7)<-.5 AND C$<>"D" THEN GOSUB 1630:RETURN
2630 GOSUB 260:LOCATE 15,30:PRINT"ENGINEERING:   RESERVE ENERGY EXHAUSTED!"
2640 LOCATE 16,30:PRINT"REROUTING SHIELD ENERGY TO RESERVES.":GOSUB 210
2650 S=S+E:E=0:IF S<=0 THEN S=0
2660 RETURN
2670 REM LRS
2680 KEY OFF:FOR LP=1 TO 10:KEY(LP)OFF:KEY LP,"±±±±±±":NEXT LP
2690 HP=3:LP=17:KEY 9," ? ? ?":ON KEY(9)GOSUB 1000:KEY(9)ON:KEY ON
2700 FOR I=Q1-1 TO Q1+1:LP=LP+2:N(1)=-1:N(2)=-2:N(3)=-3:FOR J=Q2-1 TO Q2+1
2710 IF I>0 AND I<9 AND J>0 AND J<9 THEN N(J-Q2+2)=G(I,J):Z(I,J)=G(I,J)
2720 NEXT J:LOCATE LP,31:FOR L=1 TO 3:PRINT "∞";:IF N(L)<0 THEN PRINT"***∞";:GOTO 2740
2730 PRINT RIGHT$(STR$(N(L)+1000),3);"∞";
2740 NEXT L:PRINT "∞":NEXT I
2750 RETURN
2760 REM Phaser
2770 E=E-X:IF D(8)<0 THEN X=X*RND(1):TIMER OFF
2780 I=1:FOR LP=1 TO 30:SOUND 2000,.5:SOUND 1950,1:NEXT LP:SOUND 1950,5
2790    IF K(I,3)<=0 THEN 2930
2800 H=INT(((X*5)/FND(0))*(5-K3)):'IF H>E+(X/5)+S THEN H=INT(RND(1)*(E+(X/5)+S))
2810 IF H>=6500 THEN H=INT(3000*RND+4000)
2820 IF H>K(I,3)/25 THEN 2850
2830 GOSUB 260:LOCATE 18,30:PRINT W$;"Phasers out of range. Sensors":LOCATE 19,30:PRINT"read no damage to enemy vessel ";
2840 PRINT"at (";USING"#_,#_).";K(I,2),K(I,1):GOSUB 210:GOTO 2930
2850 K(I,3)=K(I,3)-H:GOSUB 260:LOCATE 18,30:PRINT W$;INT(H);"unit hit to vessel at sector (";USING"#_,#_).";K(I,2),K(I,1)
2860 IF K(I,3)<=0 THEN GOSUB 210:GOTO 2880
2870 LOCATE 20,30:PRINT"Sensors show";INT(K(I,3));"units remaining.":GOSUB 210:GOTO 2930
2880 K3=K3-1:K9=K9-1:Z1=K(I,1):Z2=K(I,2):A$="   ":GOSUB 5290:GOSUB 4400
2890 IF K3=<0 THEN X=4802:GOSUB 11060:GOSUB 590:GOSUB 1560
2900 IF K3>0 THEN FOR LP=1 TO 150:SOUND INT(RND(1)*1000+37),.1:NEXT LP:SOUND 800,10
2910 GOSUB 260:LOCATE 18,30:PRINT W$;"Enemy ship destroyed!":GOSUB 210
2920 K(I,3)=0:G(Q1,Q2)=G(Q1,Q2)-100:Z(Q1,Q2)=G(Q1,Q2):IF K9<=0 THEN 4290
2930 I=I+1:IF I<4 THEN 2790 ELSE GOSUB 3940:GOSUB 4400:GOTO 1590
2940 REM Photons
2950 IF C1(1)>=5 THEN C1=C1-1
2960 C1=C1+C1(1)/10:H8=0:TIMER OFF
2970 IF C1>=9 THEN BEEP:C1=INT(RND(1)*7+1)
2980 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):E=E-50
2990 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):X=S1:Y=S2
3000 O.X=X1:O.Y=X2:P=P-1:S1.X=S1:S2.X=S2:IF TORP$="" THEN TORP$=" "+CHR$(249)+" "
3010 REM * TORPEDO TRACK *
3020 O.X=X:O.Y=Y:X=X+X1:Y=Y+X2:X3=INT(X+.5):Y3=INT(Y+.5)
3030 IF X3<=0 OR X3>=9 OR Y3<=0 OR Y3>=9 THEN 3500
3040 A$="   ":Z1=X:Z2=Y:GOSUB 5440
3050 IF Z3<>0 THEN 3070 ELSE IF O.X<>S1.X OR O.Y<>S2.X THEN Z1=O.X:Z2=O.Y:A$="   ":GOSUB 5280
3060 A$="*-]":Z1=X:Z2=Y:GOSUB 5440:IF Z3=0 THEN 3540 ELSE 3110
3070 REM Torp/Grid
3080 IF O.X=S1.X AND O.Y=S2.X THEN 3090 ELSE Z1=O.X:Z2=O.Y:A$="   ":GOSUB 5280
3090 IF X<>S1.X OR Y<>S2.X THEN Z1=X:Z2=Y:A$=TORP$:GOSUB 5280:GOSUB 4400
3100 ANY$="":GOTO 3020
3110 RANDOMIZE TIMER:IF INT(RND(1)*9)>=5 THEN 3170 ELSE FOR I=1 TO 3
3120 IF K(I,1)=X AND K(I,2)=Y THEN K(I,3)=INT(K(I,3)/INT(RND(1)*3 +2)):GOTO 3140 ELSE 3130
3130 NEXT I:I=1:GOTO 3170
3140 ROM=I:GOSUB 4400:BEEP
3150 GOSUB 260:LOCATE 18,30:PRINT W$;"Critical damage to Warbird at (";USING"#_,#_).";K(ROM,2),K(ROM,1):IF K(ROM,3)>=.01*E0 THEN GOSUB 210:GOSUB 3930:GOTO 1720
3160 IF O.X=S1.X AND O.Y=S2.X THEN ELSE A$="   ":GOSUB 5280:GOSUB 4400:BEEP
3170 K3=K3-1:Z1=X:Z2=Y:A$="   ":GOSUB 5280:GOSUB 4400:GOSUB 260
3180 IF K3=<0 THEN X=4802:GOSUB 11060:GOSUB 590:LOCATE 15,30:PRINT W$;"Quadrant secured.":GOSUB 1560:GOTO 3210
3190 IF K3>0 THEN FOR LP=1 TO 150:SOUND INT(RND(1)*1000+37),.1:NEXT LP:SOUND 800,10
3200 LOCATE 15,30:PRINT W$;:IF TORP$<>" "+CHR$(249)+" " THEN PRINT"Romulan vessel destroyed!" ELSE PRINT"The Romulan ship has destroyed its own!"
3210 GOSUB 210:K9=K9-1:IF K9<=0 THEN 4290
3220 FOR I=1 TO 3:IF X3=K(I,1) AND Y3=K(I,2) THEN 3240
3230 NEXT I:I=3
3240 K(I,3)=0:IF K3>3 OR K3<0 THEN K3=0:GOTO 3490 ELSE 3490
3250 A$=" * ":Z1=X:Z2=Y:GOSUB 5440:IF Z3=0 THEN 3270
3260 GOSUB 4410:GOSUB 260:LOCATE 15,30:PRINT W$;"Torpedo disintegrated into":LOCATE 16,30:PRINT"planet's atmosphere at (";USING"#_,#_).";Y3,X3:GOSUB 210:GOSUB 3940:GOTO 1590
3270 A$=" "+CHR$(15)+" ":Z1=X:Z2=Y:GOSUB 5440:IF Z3=0 THEN 3290
3280 GOSUB 4410:GOSUB 260:LOCATE 15,30:PRINT W$;"Star at (";USING"#_,#_) ";Y3,X3;:PRINT"absorbed torpedo energy.":GOSUB 210:GOSUB 3940:GOTO 1590
3290 A$="<+>":Z1=X:Z2=Y:GOSUB 5440:IF Z3=0 THEN 3010
3300 IF O.X=S1.X AND O.Y=S2.X THEN ELSE A$="   ":Z1=O.X:Z2=O.Y:GOSUB 5280
3310 SB.SHE=SB.SHE-1:IF SB.SHE>0 THEN GOSUB 4400:SOUND 100,1:SOUND 32767,1:SOUND 100,8 ELSE 3390
3320 IF LEFT$(TORP$,1)="P" THEN X$="Phaser" ELSE X$="Torpedo"
3330 GOSUB 260:LOCATE 14,30:PRINT D$;X$;" damage to starbase";STB;".":LOCATE 15,30:PRINT"Their shields are at ";USING "###_%.";INT(24*SB.SHE+4)
3340 LOCATE 16,30:IF SB.SHE<=1 THEN PRINT WC$;"Captain, starbase";STB;"has issued a":LOCATE 17,30:PRINT"distress signal. They report they cannot with-":LOCATE 18,30:PRINT"stand anymore damage. ":GOTO 3360
3350 PRINT WC$;"Message from starbase ";USING"###_.";STB:LOCATE 17,30
3360 LOCATE 19,30:IF MID$(TORP$,2,1)=CHR$(249) OR TORP$="PR" THEN PRINT"They're asking us for assistance." ELSE PRINT"They're asking why we're attacking."
3370 IF K3<=0 THEN LOCATE 20,30:PRINT R$;:IF SB.SHE>1 THEN PRINT"Are you out of your mind!" ELSE PRINT"Captain! What the hell are you doing?!"
3380 TD=60:GOSUB 320:GOTO 1720
3390 Z1=X:Z2=Y:A$="   ":GOSUB 5280:GOSUB 4400:GOSUB 260
3400 X=0:GOSUB 11060:IF K3>0 THEN GFX$="00I":GOSUB 11030
3410 Z1=X:Z2=Y:A$="   ":GOSUB 5280:GOSUB 4400:GOSUB 260
3420 LOCATE 14,30:PRINT W$;"Starbase destroyed!":B3=B3-1:B9=B9-1:IF C$="D" THEN IF K3>0 THEN C$="R" ELSE C$="G"
3430 IF MID$(TORP$,2,1)=CHR$(236) THEN LOCATE 16,30:PRINT D$;"Sir, I fail to see the logic in...":LOCATE 17,30:PRINT R$;"You're damn right it's illogical!"
3440 IF B9>0 OR K9>ST-T0-T9 THEN 3470
3450 LOCATE 18,30:PRINT R$;"I'm relieving you of command..."
3460 GOSUB 210:GOTO 4210
3470 LOCATE 18,30:PRINT"DR.CRUSHER: Those were innocent people!":IF K3>0 THEN LOCATE 19,30:PRINT R$;"I'll have to notify Starfleet.":D0=0
3480 TD=45:GOSUB 320
3490 G(Q1,Q2)=K3*100+B3*10+S3:Z(Q1,Q2)=G(Q1,Q2):GOSUB 3940:GOTO 1590
3500 Z1=O.X:Z2=O.Y:A$="   ":GOSUB 5290:GOSUB 4400:GOSUB 260
3510 LOCATE 16,30:PRINT W$;:IF TORP$=" "+CHR$(249)+" " THEN PRINT"Their torpedo missed"; ELSE PRINT"Photon torpedo missed";
3520 PRINT" and self-destructed":LOCATE 17,30:PRINT"at quadrant border."
3530 GOSUB 210:GOSUB 3940:GOTO 1590
3540 REM Hit?
3550 A$="=-@":Z1=X:Z2=Y:GOSUB 5440:IF Z3=0 THEN 3250 ELSE GOSUB 4400
3560 SOUND 400,2:SOUND 300,1:SOUND 200,1:SOUND 100,1:SOUND 75,1:SOUND 50,1:SOUND 57,10
3570 H=INT(K(LP1,3)/3):S=S-H:K(LP1,3)=K(LP1,3)-(H/3)
3580 LOCATE 18,30:PRINT USING"####_ unit hit on Enterprise!";H
3590 H8=1:GOSUB 4080:GOSUB 3600:GOTO 1720
3600 REM ROMs MOVE/DON'T FIRE
3610 TIMER OFF:FOR I=1 TO K3:IF K(I,3)<=0 THEN 3640
3620 A$="   ":Z1=K(I,1):Z2=K(I,2):GOSUB 5290:GOSUB 5260
3630 K(I,1)=Z1:K(I,2)=Z2:A$="*-]":GOSUB 5290
3640 NEXT I:H8=0:GOSUB 4400:TIMER ON:RETURN
3650 REM ROM's FIRST?
3660 IF INT(RND(1)*9)>3 OR K3<=0 OR T0=ST THEN RETURN
3670 GOSUB 260:LOCATE 14,30:PRINT W$;"They're attacking first!!!":GOSUB 210
3680 IF INT(RND(0)*3)=2 THEN GOSUB 10610:GOTO 1590
3690 GOSUB 3960:GOTO 1590
3700 REM Repair
3710 GOSUB 260:KEY OFF:D3=0:FOR I=1 TO 8:IF D(I)<0 THEN D3=D3+.1
3720 NEXT I:IF D3=0 THEN 3910
3730 PRINT:D3=D3+D4:IF D3>=1 THEN D3=.9
3740 LOCATE 14,30:PRINT L$;"Captain, starbase repair techn-"
3750 LOCATE 15,30:PRINT"icians are standing by to effect repairs to the "
3760 LOCATE 16,30:PRINT"Enterprise. Estimated time needed to complete"
3770 LOCATE 17,30:PRINT"repairs is";.01*INT(100*D3);"stardates."
3780 LOCATE 18,34:PRINT"Speaking freely, Captain, my past experiences"
3790 LOCATE 19,30:PRINT"with starbase technicians have shown a tendency"
3800 LOCATE 20,30:PRINT"to underestimate when it comes to starships."
3810 LOCATE 22,34:PRINT"Starbase";INT(100*(Q2/Q1));"is requesting your approval,"
3820 LOCATE 23,30:PRINT"Sir. Will you authorize the repair?"
3830 ANY$=INKEY$:IF ANY$="" THEN 3830 ELSE IF ANY$="N" OR ANY$="n" THEN RETURN
3840 IF ANY$="Y" OR ANY$="y" THEN 3850 ELSE 3830
3850 GOSUB 260
3860 LOCATE 14,30:PRINT L$;"I'll notify them immediately!"
3870 FOR I=1 TO 8:IF D(I)<0 THEN D(I)=0:IF I=2 THEN GOSUB 4400
3880 NEXT I:ST=ST+(D3+INT(RND(1)*10)/10)-1
3890 GOSUB 210:GOSUB 9660:RETURN
3900 REM
3910 LOCATE 16,30:PRINT L$;"All systems functional."
3920 GOSUB 210:RETURN
3930 REM ROMs SHOOTING *
3940 IF K3<=0 THEN TIMER OFF:RETURN ELSE TIMER ON:IF TORP$=" "+CHR$(249)+" " THEN GOSUB 3600:RETURN ELSE X=INT(RND(1)*9+1)
3950 IF X=5 THEN GOSUB 3600:RETURN ELSE IF X>5 THEN 10610
3960 IF D0<>0 THEN GOSUB 260:LOCATE 16,30:PRINT W$;"They're powering up their disruptors!":GOSUB 4010:LOCATE 18,30:PRINT WC$;"Starbase shields the Enterprise.":GOSUB 210:IF INT(RND(1)*10)>6 THEN TORP$="PR":GOTO 3310 ELSE RETURN
3970 GOSUB 4400
3980 X=INT(RND(1)*3+1):H=INT(RND(1)*200+1):IF X>3 THEN X=3
3990 I=1:GOSUB 260:LOCATE 15,30:PRINT W$;"They're powering up their disruptors!":LOCATE 17,30:PRINT R$;"Brace yourselves!":TD=3:GOSUB 320
4000 IF K(I,3)<=0 THEN 4180 ELSE IF INT(RND(1)*5)=3 THEN PLAY"O1D7":H=.01*K(I,3):GOTO 4040 ELSE GOSUB 4010:GOTO 4020
4010 FOR LP=1 TO 30:SOUND 1500,.5:SOUND 1510,1:NEXT LP:RETURN
4020 H=INT((K(I,3)/FND(1))*RND(1)+1000):IF H>E0/2 THEN H=E0/2*RND(1) ELSE IF INT(RND(0)*10)=RND(1)*10 THEN H=100
4030 SOUND 400,2:SOUND 300,1:SOUND 200,1:SOUND 100,1:SOUND 75,1:SOUND 50,1:SOUND 57,10
4040 S=S-H:K(I,3)=K(I,3)-INT(H/2):IF K(I,3)<=0 THEN ROM=I ELSE ROM=0
4050 IF H>100 THEN 4070
4060 GOSUB 260:LOCATE 17,30:PRINT W$;"Minimal damage from sector (";USING"#_,#_).";K(I,2),K(I,1):IF S<0 THEN S=100:GOTO 4080 ELSE 4080
4070 GOSUB 260:LOCATE 17,30:PRINT W$;INT(H);"unit hit on Enterprise":LOCATE 18,30:PRINT"from sector (";USING"#_,#_).";K(I,2),K(I,1)
4080 IF S>0 THEN 4120 ELSE IF C$<>"D" THEN 360
4090 S=0:E=E0:LOCATE 20,30:PRINT WC$;"Starbase shields holding."
4100 LOCATE 22,30:PRINT W$;"All decks reporting no damage."
4110 TD=60:GOSUB 320:IF INT(RND(1)*10)>5 THEN GOSUB 3600:GOTO 1720 ELSE 1720
4120 LOCATE 20,30:PRINT D$;"Shields now at";USING"###.#_ %.";(S/E0)*100:GOSUB 210:IF H<500  THEN 4180
4130 IF RND(1)>.6 OR H/S<=.02 THEN 4180
4140 R1=FNR(1):D(R1)=D(R1)-H/S-.5*RND(1):GOSUB 5340
4150 GOSUB 260:LOCATE 15,30:PRINT L$;G2$:LOCATE 16,30:PRINT"damaged by the hit.":GOSUB 210:IF G2$="Short Range Sensors" THEN GOSUB 4400:GOTO 4180
4160 IF ROM=0 THEN 4190 ELSE GOSUB 260:SOUND 1000,1:SOUND 32767,1:SOUND 1000,8:LOCATE 14,30:PRINT D$;"Sensors show a warpcore breach":LOCATE 15,30:PRINT"at (";USING"#_,#_) is in progress.";K(ROM,2),K(ROM,1):K(ROM,3)=0
4170 TORP$="ƒÏƒ":X=K(ROM,1):Y=K(ROM,2):ROM=0:GOTO 3170
4180 IF INT(RND(1)*10)<=3 THEN GOSUB 3600:GOTO 4160 ELSE 4160
4190 I=I+1:IF I<4 THEN 4000
4200 H8=0:RETURN
4210 REM Game Over
4220 CLS:TIMER OFF
4230 CHAIN "004",,ALL
4240 REM Destroyed
4250 TIMER OFF:CLS:PRINT"THE ENTERPRISE HAS BEEN DESTROYED. THE FEDERATION ";
4260 PRINT"WILL SUFFER A GREAT LOSS, BUT THE WAR CONTINUES..."
4270 ANY$=INKEY$:IF ANY$="" THEN 4270 ELSE 4230
4280 REM WINNER
4290 TIMER OFF:LP=104:GOTO 4210
4300 REM SCAN 4 STB
4310 FOR I=S1-1 TO S1+1:FOR J=S2-1 TO S2+1
4320 IF INT(I+.5)<1 OR INT(I+.5)>8 OR INT(J+.5)<1 OR INT(J+.5)>8 THEN 4340
4330 A$="<+>":Z1=I:Z2=J:GOSUB 5440:IF Z3=1 THEN 4350
4340 NEXT J:NEXT I:D0=0:GOTO 4370
4350 D0=1:E=E0:RES=FNPOW(E):SHE=0:SHD=0:USED=0:P=P0:GOSUB 720
4360 LOCATE 16,30:PRINT"Shields dropped for docking.":GOSUB 210:S=0:E=E0:GOTO 4410
4370 IF K3>0 THEN C$="R":GOTO 4410
4380 IF K3<=0 AND C$="D" THEN C$="":GOSUB 590
4390 C$="G":IF E<E0*.1 THEN C$="Y"
4400 REM Mapping
4410 CMD$="X"
4420 SOUND 1000,1:IF D(2)>=0 THEN 4440 ELSE X$(0)=CHR$(176):X$(1)=CHR$(177):X$(2)=CHR$(178):X$(3)=CHR$(219):X$(4)=CHR$(222):X$(5)=CHR$(221):X$(6)=CHR$(32)
4430 FOR LP=16 TO 22 STEP 2:LOCATE LP,4:FOR LP1=1 TO 24:PRINT X$(INT(RND(1)*6));:NEXT LP1,LP:FOR LP=23 TO 17 STEP -2:LOCATE LP,4:FOR LP1=1 TO 24:PRINT X$(INT(RND(1)*6));:NEXT LP1,LP:RETURN
4440 REM SRS MAP
4450 IF CMD$="X" THEN CMD$="" ELSE GOSUB 4400
4460 FOR I.1=1 TO 8:MP2$="":MAP$(I.1)=""
4470 FOR J.1=(I.1-1)*24+1 TO (I.1-1)*24+22 STEP 3:MP1$=MID$(Q$,J.1,3)
4480 IF MP1$="   " AND I.1<>8 THEN MP1$=MG$
4490 IF MP1$="   " AND I.1=8 THEN MP1$=CHR$(196)+CHR$(193)+CHR$(196)
4500 MP2$=MP2$+MP1$
4510 NEXT J.1
4520 MAP$(I.1)=MP2$:NEXT I.1:SOUND 1000,1
4530 FOR LP=16 TO 23:LOCATE LP,4:PRINT MAP$(LP-15):NEXT LP:RETURN
4540 REM QUIT
4550 GOSUB 260:KEY OFF:FOR LP=1 TO 10:KEY LP,"±±±±±±":KEY(LP)OFF:NEXT LP
4560 KEY ON:LOCATE 15,30:PRINT"COUNSELOR TROI: Captain, I sense you're stressed."
4570 LOCATE 16,30:PRINT"I think this mission is too much for you. Perhaps"
4580 LOCATE 17,30:PRINT"some shore leave. You can always return later."
4590 KEY 1,"  NO  ":ON KEY(1)GOSUB 5740:KEY(1)ON
4600 KEY 10,"  YES ":ON KEY(10)GOSUB 4620:KEY(10)ON
4610 KEY ON:GOTO 6220
4620 GOSUB 260:LOCATE 15,30:PRINT R$;"The Excelsior is standing by."
4630 LOCATE 16,30:PRINT"We will be transfering our orders to them if you"
4640 LOCATE 17,30:PRINT"decide to abort the mission. Do you want a new"
4650 LOCATE 18,30:PRINT"assignment now or would you rather retire for the"
4660 LOCATE 19,30:PRINT"moment and return later to another mission, Sir?"
4670 KEY 1,"NeverM":ON KEY(1)GOSUB 5740:KEY(1)ON
4680 KEY 5,"  NEW ":ON KEY(5)GOSUB 4710:KEY(5)ON
4690 KEY 10," QUIT ":ON KEY(10)GOSUB 4720:KEY(10)ON
4700 KEY ON:GOTO 6220:RETURN
4710 GOSUB 260:KEY OFF:RUN"002":RETURN
4720 KEY OFF:CLS:GOTO 4210:RETURN
4730 REM RECORD
4740 KEY OFF:FOR LP=1 TO 10:KEY(LP)OFF:KEY LP,"±±±±±±":NEXT LP:GOSUB 930:IF Z3=1 THEN RETURN ELSE KEY 9," ? ? ?":HP=4:ON KEY(9)GOSUB 1000:KEY(9)ON:KEY ON
4750 LOCATE 14,30:PRINT"(_∫€_CUMULATIVE_GALACTIC_RECORD_€€€€€€€€€€€€€€€∫_)"
4760 LOCATE 15,30:PRINT"     1     2     3     4     5     6     7     8  "
4770 LP=15:FOR I=1 TO 8:LP=LP+1:LOCATE LP,30:IF H8=0 THEN 4860
4780 FOR J=1 TO 8:IF I=Q1 AND J=Q2 THEN 4810
4790 IF J=1 THEN PRINT"  ";RIGHT$(STR$(I),1);" ";:GOTO 4830
4800 PRINT CHR$(196)+CHR$(197)+CHR$(196);:GOTO 4830
4810 IF J=1 THEN PRINT"  "+RIGHT$(STR$(I),1)+CHR$(26)+RIGHT$(STR$(Z(I,J)+1000),3);:GOTO 4850
4820 PRINT CHR$(196)+CHR$(197)+CHR$(26);RIGHT$(STR$(Z(I,J)+1000),3);:GOTO 4850
4830 IF Z(I,J)=0 THEN PRINT STRING$(3,196);:GOTO 4850
4840 PRINT RIGHT$(STR$(Z(I,J)+1000),3);
4850 NEXT J:GOTO 4880
4860 Z4=I:Z5=1:GOSUB 5490:J0=INT(15-.5*LEN(G2$)):PRINT TAB(J0);G2$;
4870 Z5=5:GOSUB 5490:J0=INT(39-.5*LEN(G2$)):PRINT TAB(J0);G2$;
4880 NEXT I
4890 TD=60:GOSUB 320
4900 RETURN
4910 REM STATUS
4920 GOSUB 190:HP=8:CHAIN"003",,ALL
4930 GOSUB 190:HP=9:CHAIN"003",,ALL
4940 REM CGC
4950 IF K3<=0 THEN GOSUB 260:LOCATE 14,30:PRINT W$;"Sensors read no enemy":LOCATE 15,30:PRINT"vessels in the quadrant.":GOSUB 210:RETURN
4960 GOSUB 10360
4970 LOCATE 19,33:PRINT K3
4980 REM =-@ 2 *-]
4990 H8=0:FOR I=1 TO 3:IF K(I,3)<=0 THEN 5030 ELSE KEY ON
5000 LOCATE 17,33:PRINT I:W1=K(I,1):X=K(I,2):LOCATE 21,62:PRINT X;",";W1
5010 C1=S1:A=S2:LOCATE 21,34:PRINT A;",";C1:GOSUB 5070
5020 ANY$=INKEY$:IF ANY$=CHR$(13) OR ANY$=" " THEN BEEP ELSE 5020
5030 NEXT I:RETURN
5040 REM ROMs CALC TO ENT
5050 IF B3>0 THEN W1=B4:X=B5 ELSE W1=S1:X=S2
5060 C1=K(LP1,1):A=K(LP1,2):TORP$=" "+CHR$(249)+" "
5070 X=X-A:A=C1-W1:IF X<0 THEN 5150
5080 IF A<0 THEN 5170
5090 IF X>0 THEN 5110
5100 IF A=0 THEN C1=5:GOTO 5120
5110 C1=1
5120 IF ABS(A)<=ABS(X) THEN 5140
5130 DIR=C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A)):GOTO 5210
5140 DIR=C1+(ABS(A)/ABS(X)):GOTO 5210
5150 IF A>0 THEN C1=3:GOTO 5180
5160 IF X<>0 THEN C1=5:GOTO 5120
5170 C1=7
5180 IF ABS(A)>=ABS(X) THEN 5200
5190 DIR=C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X)):GOTO 5210
5200 DIR=C1+(ABS(X)/ABS(A))
5210 DIST=INT(SQR(X^2+A^2))
5220 IF H8=0 THEN LOCATE 17,70:PRINT USING"#.#";DIR:LOCATE 19,51:PRINT USING "#";DIST
5230 IF H8=0 THEN LOCATE 23,50:PRINT USING"###";INT((K(I,3)/E0)*100)
5240 RETURN:GOTO 1720
5250 REM Vacancy in Quad
5260 R1=FNR(1):R2=FNR(1):A$="   ":Z1=R1:Z2=R2:GOSUB 5440:IF Z3=0 THEN 5260
5270 RETURN
5280 REM Insert
5290 S8=INT(Z2-.5)*3+INT(Z1-.5)*24+1
5300 IF LEN(A$)<>3 THEN A$="???"
5310 IF S8=1 THEN Q$=A$+RIGHT$(Q$,189):RETURN
5320 IF S8=190 THEN Q$=LEFT$(Q$,189)+A$:RETURN
5330 Q$=LEFT$(Q$,S8-1)+A$+RIGHT$(Q$,190-S8):RETURN
5340 ON R1 GOTO 5350,5360,5370,5380,5390,5400,5410,5420
5350 G2$="Warp Engines":RETURN
5360 G2$="Short Range Sensors":RETURN
5370 G2$="Long Range Sensors":RETURN
5380 G2$="Phaser Control":RETURN
5390 G2$="Photon Tubes":RETURN
5400 G2$="Damage Control":RETURN
5410 G2$="Shield Control":RETURN
5420 G2$="Library-Computer":RETURN
5430 REM ($) Compare
5440 Z1=INT(Z1+.5):Z2=INT(Z2+.5):S8=(Z2-1)*3+(Z1-1)*24+1:Z3=0
5450 IF MID$(Q$,S8,3)<>A$ THEN RETURN
5460 Z3=1:RETURN
5470 REM QUAD. NAME IN G2$ FROM Z4,Z5=(Q1,Q2)
5480 REM CALL WITH G5=1 TO GET REGION NAME ONLY
5490 IF Z5<=4 THEN ON Z4 GOTO 5510,5520,5530,5540,5550,5560,5570,5580
5500 GOTO 5590
5510 G2$="Antares":GOTO 5680
5520 G2$="Rigel":GOTO 5680
5530 G2$="Procyon":GOTO 5680
5540 G2$="Vega":GOTO 5680
5550 G2$="Canopus":GOTO 5680
5560 G2$="Altair":GOTO 5680
5570 G2$="Sagittarius":GOTO 5680
5580 G2$="Pollux":GOTO 5680
5590 ON Z4 GOTO 5600,5610,5620,5630,5640,5650,5660,5670
5600 G2$="Sirius":GOTO 5680
5610 G2$="Deneb":GOTO 5680
5620 G2$="Capella":GOTO 5680
5630 G2$="Betelgeuse":GOTO 5680
5640 G2$="Aldebaran":GOTO 5680
5650 G2$="Regulus":GOTO 5680
5660 G2$="Arcturus":GOTO 5680
5670 G2$="Spica":GOTO 5680
5680 IF G5<>1 THEN ON Z5 GOTO 5700,5710,5720,5730,5700,5710,5720,5730
5690 RETURN
5700 G2$=G2$+" I  ":RETURN
5710 G2$=G2$+" II ":RETURN
5720 G2$=G2$+" III":RETURN
5730 G2$=G2$+" IV ":RETURN
5740 REM PANEL STATUS
5750 TORP$="":X$="":LINE (276,74)-(355,76),0,BF
5760 KEY OFF:LOCATE 14,3:PRINT SPACE$(26):FOR LP=1 TO 10:KEY(LP)OFF:NEXT LP
5770 IF C$="D" THEN E=E0:S=0
5780 IF E+S>10000 THEN S=9000:E=1000
5790 IF E<0 THEN GOSUB 2610
5800 IF S<0 THEN S=0:E=E+S:IF E>E0 THEN E=E0:GOTO 5790 ELSE 5790
5810 IF E+S<=1000 THEN GOSUB 1830
5820 IF E<E0*.1 AND D(7)<0 THEN GOSUB 10490:LOCATE 15,35:PRINT"COMPUTER:±±WARNING":LOCATE 20,49:PRINT"ENERGY RESERVES":LOCATE 21,49:PRINT"DANGEROUSLY LOW" ELSE 5840
5830 GOSUB 320:GOSUB 10490:LOCATE 20,49:PRINT"REPAIR SHIELD CONTROL":LOCATE 21,49:PRINT"ALLOT RESERVE POWER":SOUND 800,1:SOUND 32767,1:SOUND 800,8:TD=20:GOSUB 320
5840 IF DL-ST>10 THEN 5850 ELSE GOSUB 260:BEEP:LOCATE 14,30:PRINT R$;"Sir, our deadline is in";INT(DL-ST);"stardate";:IF INT(DL-ST)>1 THEN PRINT"s.":GOSUB 320 ELSE PRINT".":GOSUB 320
5850 GOSUB 6100
5860 GOSUB 9740:LOCATE 14,3:PRINT"SHORT RANGE SENSOR SCAN  "+CHR$(25)
5870 LOCATE 17,69:PRINT USING"#####.#";ST
5880 LOCATE 20,69:PRINT USING"#####.#";DL
5890 LOCATE 22,39:PRINT REG$:PERI.REG$=REG$
5900 LOCATE 22,63:PRINT USING"#_,#";Q2,Q1
5910 LOCATE 22,75:PRINT USING"#_,#";S2,S1
5920 CND$=CHR$(222)+" "+CHR$(221):IF K3>0 AND C$<>"D" THEN C$="R"
5930 IF K3<=0 AND C$<>"D" THEN C$="G"
5940 IF K3>0 THEN LOCATE 18,53:PRINT CND$
5950 IF C$="Y" THEN LOCATE 19,53:PRINT CND$:IF E>=E0*.1 THEN LOCATE 19,53:PRINT STRING$(3,177):C$="G"
5960 IF C$="G" THEN LOCATE 20,53:PRINT CND$
5970 IF C$="D" THEN LOCATE 17,53:PRINT CND$
5980 IF C$="G" AND E<E0*.1 THEN C$="Y":LOCATE 20,53:PRINT STRING$(3,177):LOCATE 19,53:PRINT CND$
5990 REM READOUT
6000 IF C$="D" THEN E=E0:S=0:IF K3>0 THEN LOCATE 18,53:PRINT CND$
6010 RES=FNPOW(E):RES1=2*RES
6020 SHD=FNPOW(S):WST=ABS(INT(50-(RES+SHD))):USED=WST*2:SHE1=2*SHD
6030 LINE (304,136)-(304+SHE1,142),1,BF
6040 LINE (304+SHE1+1,136)-(407,142),0,BF:LINE (304+RES1+1,144)-(407,150),0,BF
6050 LINE (304+USED+1,128)-(407,134),0,BF:LINE(304+SHE1+RES1+1,152)-(407,158),0,BF
6060 LINE (304,144)-(304+RES1,150),1,BF
6070 LINE (304,152)-(304+RES1+SHE1,158),1,BF
6080 LINE (304,128)-(304+USED,134),1,BF
6090 RETURN
6100 KEY OFF:KEY 1,"NAVIG":ON KEY(1)GOSUB 6380 :KEY(1)ON
6110 KEY 2,"SHIELD":ON KEY(2)GOSUB 7250:KEY(2)ON
6120 KEY 3,"TACTIC":ON KEY(3)GOSUB 7810:KEY(3)ON
6130 KEY 4,"ENGINE":ON KEY(4)GOSUB 8790:KEY(4)ON
6140 KEY 8,"STATUS":ON KEY(8)GOSUB 4930:KEY(8)ON
6150 KEY 6,"SCANS":ON KEY(6)GOSUB 7740:KEY(6)ON
6160 KEY 9,"ORDERS":ON KEY(9)GOSUB 4910:KEY(9)ON
6170 KEY 5,"S.R.S.":ON KEY(5)GOSUB 11420:KEY(5)ON
6180 KEY 7,"±HELP±":ON KEY(7)GOSUB 7710:KEY(7)ON
6190 KEY 10,"±ABORT":ON KEY(10)GOSUB 4540:KEY(10)ON
6200 KEY ON:CMD$="":TIMER ON
6210 RETURN
6220 REM---L.E.D.s---
6230 GOSUB 6320:IF C$="G" OR C$="D" AND K3<=0 THEN FLIP=1
6240 FOR VLP=312 TO 277 STEP -5:FOR VL1=1 TO 0 STEP -1
6250 CIRCLE (VLP,76),1,VL1:CIRCLE (639-VLP-8,76),1,VL1:TM=TIMER
6260 IF TIMER<TM+.01 THEN 6260
6270 NEXT VL1,VLP
6280 GOTO 6220
6290 REM ALERT PANELS
6300 FOR LP2=5 TO 7:FOR LP3=3 TO 76 STEP 73:LOCATE LP2,LP3:PRINT PANEL$:NEXT LP3,LP2:LINE (300,74)-(332,75),ABS(FLIP-1),BF
6310 PANEL$=" ":RETURN
6320 IF C$="" OR C$="G" THEN PANEL$=CHR$(177):GOTO 6300
6330 IF C$="D" AND K3<=0 THEN PANEL$=CHR$(177):GOTO 6300
6340 IF FLIP=1 THEN FLIP=0:PANEL$=" ":GOTO 6300
6350 IF FLIP=0 THEN FLIP=1
6360 IF C$="Y" THEN PANEL$=CHR$(178):GOTO 6300
6370 IF C$="R" OR C$="D" AND K3>0 THEN PANEL$=CHR$(176):GOTO 6300 ELSE RETURN
6380 REM NAV
6390 GOSUB 260:KEY OFF
6400 FOR LP=2 TO 4:KEY LP,"±±±±±±":KEY(LP)OFF:NEXT LP
6410 KEY 5,"RESET":ON KEY(5)GOSUB 6890:KEY(5)ON
6420 KEY 6,"SCANS":ON KEY(6)GOSUB 7650:KEY(6)ON
6430 KEY 7,"RECORD":ON KEY(7)GOSUB 7670:KEY(7)ON
6440 KEY 8,"CHART":ON KEY(8)GOSUB 7690:KEY(8)ON
6450 KEY 9," ? ? ?":ON KEY(9)GOSUB 6990:KEY(9)ON
6460 KEY 10,"ENERGY":ON KEY(10)GOSUB 6910:KEY(10)ON
6470 KEY 1,"EXIT":ON KEY(1)GOSUB 6900:KEY(1)ON
6480 KEY ON:GOSUB 9870:KEY ON
6490 LOCATE 18,70:PRINT USING"#####.#";ST:LOCATE 20,70:PRINT USING"#####.#";DL
6500 LOCATE 14,3:PRINT SPACE$(26):LOCATE 14,3:PRINT"REGION: ";REG$
6510 LOCATE 22,42:PRINT USING"#";Q2;:PRINT USING"_,#";Q1
6520 LOCATE 23,42:PRINT USING"#";S2;:PRINT USING"_,#";S1
6530 LOCATE 22,48:PRINT SPACE$(31):LOCATE 22,48:PRINT"ENTER COURSE # w/IN RANGE 1-8"
6540 LOCATE 17,34:PRINT" ":N.C1$=INKEY$:LOCATE 17,34:PRINT CHR$(219):IF N.C1$="" THEN 6540 ELSE IF VAL(N.C1$)<1 OR VAL(N.C1$)>8 THEN 6540
6550 SOUND 1000,1:LOCATE 17,34:PRINT N.C1$
6560 LOCATE 22,48:PRINT SPACE$(31):LOCATE 22,48:PRINT"ENTER DISTANCE TO NEW LOCATION"
6570 LOCATE 17,51:PRINT" ":N.W1$=INKEY$:LOCATE 17,51:PRINT CHR$(219):IF N.W1$="" THEN 6570 ELSE IF VAL(N.W1$)<1 OR VAL(N.W1$)>9 THEN 6570
6580 SOUND 1000,1:LOCATE 17,51:PRINT N.W1$
6590 REM VEL
6600 H8=0:VEL$="":LOCATE 22,48:PRINT SPACE$(31):LOCATE 22,48:IF D(1)>=0 THEN PRINT"(W)arp  or  (I)mpulse factor?":X$="WI" ELSE PRINT"*NOTICE| DAMAGE: (I)mpulse ONLY":X$="I"
6610 LOCATE 22,47:PRINT CHR$(16):ANY$=INKEY$:LOCATE 22,47:PRINT CHR$(179):IF ANY$="" THEN 6610 ELSE IF X$="I" THEN IF ANY$="I" OR ANY$="i" THEN VEL$="I" ELSE BEEP:GOTO 6610
6620 LOCATE 22,47:PRINT CHR$(179):X$="":IF ANY$="W" OR ANY$="w" THEN VEL$="W" ELSE IF ANY$="I" OR ANY$="i" THEN VEL$="I" ELSE IF VEL$="" THEN 6610
6630 LOCATE 18,53:IF VEL$="W" THEN PRINT"_WARP___" ELSE PRINT"_IMPULSE_":VEL$="I"
6640 IF VEL$="I" THEN 6840
6650 REM WARP
6660 LOCATE 22,48:PRINT SPACE$(31):LOCATE 22,48
6670 LOCATE 22,48:PRINT"SELECT WARP FACTOR: use ["+CHR$(24)+CHR$(25)+"] "
6680 LOCATE 18,49:PRINT CHR$(16):VEL=8:N.C1=VAL(N.C1$):N.W1=VAL(N.W1$)
6690 VEL=VEL+1:IF VEL=>10 THEN VEL=1
6700 IF D(1)<0 THEN VEL=1
6710 LOCATE 18,51:PRINT USING"#";INT(VEL)
6720 LOCATE 20,54:PRINT USING"#####.#";ST+N.W1/VEL
6730 ANY$=INKEY$
6740 IF ANY$="" THEN 6730 ELSE LOCATE 18,51:PRINT USING"#";INT(VEL)
6750 IF ANY$=CHR$(0)+"H" THEN 6690
6760 IF ANY$=CHR$(0)+"P" AND VEL>1 THEN VEL=VEL-1:GOTO 6700
6770 IF ANY$=CHR$(0)+"P" AND VEL<=1 THEN VEL=9:GOTO 6700
6780 IF ANY$=CHR$(13) THEN SOUND 1000,1:GOTO 6800
6790 GOTO 6700
6800 LOCATE 18,49:PRINT"_":LOCATE 18,51:PRINT USING"#";INT(VEL)
6810 N.E=FNE(E,VEL,N.W1):LOCATE 22,48:PRINT SPACE$(31)
6820 C1=N.C1:W1=N.W1:LOCATE 14,3:PRINT SPACE$(26)
6830 GOSUB 260:GOTO 1980:RETURN
6840 REM IMPULSE
6850 LOCATE 22,48:PRINT SPACE$(31):LOCATE 18,49:PRINT"_":ST=ST+.01*N.W1
6860 C1=VAL(N.C1$):W1=INT(VAL(N.W1$))/10:LOCATE 20,54:PRINT USING"#####.#";ST
6870 VEL=1:GOSUB 250:GOTO 1980:RETURN
6880 KEY(5)ON:GOSUB 9880:GOTO 6490:RETURN 'RESET
6890 GOTO 6400:RETURN 'NAV
6900 GOSUB 250:GOTO 1720:RETURN 'EX
6910 KEY(10)ON 'E READOUT
6920 GOSUB 9990
6930 RES=FNPOW(E):SHD=FNPOW(S):WST=ABS(INT(50-(RES+SHD)))
6940 LINE (248,175)-(352,175-SHD),1,BF
6950 LINE (384,175)-(488,175-RES),1,BF
6960 LINE (520,175)-(623,175-WST),1,BF
6970 GOSUB 210
6980 GOTO 6890:RETURN
6990 HP=1:GOTO 1000 'PLOT COURSE
7000 REM CHART
7010 KEY OFF:FOR LP=1 TO 10:KEY LP,"±±±±±±":KEY(LP)OFF:NEXT LP:GOSUB 260
7020 KEY 9," ? ? ?":ON KEY(9)GOSUB 1000:HP=2:KEY(9)ON:KEY ON
7030 LOCATE 14,30:PRINT"(_∫€€€€€€€€€€€€€€_THE__GALAXY_€€€€€€€€€€€€€€€€∫_)"
7040 LOCATE 15,30:PRINT"           ⁄ƒƒ1ƒƒ2ƒƒ3ƒƒ4ƒ¬5ƒƒ6ƒƒ7ƒƒ8ƒƒø          "
7050 LOCATE 16,30:PRINT"ANTARES ƒƒƒ¥1";:X$=" I II III IV≈I II III IV":PRINT X$+"1√ƒƒƒ SIRIUS"
7060 LOCATE 17,30:PRINT"RIGEL ƒƒƒƒƒ¥2"+X$+"2√ƒƒƒƒ DENEB"
7070 LOCATE 18,30:PRINT"PROCYON ƒƒƒ¥3"+X$+"3√ƒƒ CAPELLA"
7080 LOCATE 19,30:PRINT"VEGA ƒƒƒƒƒƒ¥4"+X$+"4≥BETELGEUSE"
7090 LOCATE 20,30:PRINT"CANOPUS ƒƒƒ¥5"+X$+"5√ ALDEBARAN"
7100 LOCATE 21,30:PRINT"ALTAIR ƒƒƒƒ¥6"+X$+"6√ƒƒ REGULUS"
7110 LOCATE 22,30:PRINT"SAGITTARUIS≥7"+X$+"7√ƒ ARCTURUS"
7120 LOCATE 23,30:PRINT"POLLUX ƒƒƒƒŸ8"+X$+"8¿ƒƒƒƒ SPICA"
7130 TD=60:GOSUB 320:RETURN
7140 REM LRS
7150 GOSUB 10490:KEY(6)ON:ON KEY(9)GOSUB 1000:KEY(9)ON:TD=60
7160 IF D(3)>=0 THEN 7180 ELSE LOCATE 21,49:PRINT"SENSORS INOPERABLE":PLAY"O0MSD10D4"
7170 GOSUB 320:RETURN
7180 LOCATE 15,35:PRINT"LONG±RANGE±SENSORS":LOCATE 21,49:PRINT CHR$(17)+"ÕÕÕµ SENSOR DATA"
7190 LOCATE 23,54:PRINT"€QUADRANT:€€(";USING"#_,#_)";Q2,Q1:LOCATE 17,47:PRINT"#ENEMY≥#STARBASES≥#BODIES"
7200 GOSUB 2680 'SCANNING
7210 GOSUB 320:RETURN
7220 REM RECORD
7230 GOSUB 260:KEY(7)ON:ON KEY(9)GOSUB 1000:KEY(9)ON
7240 H8=1:GOSUB 4730:RETURN
7250 REM SHD CTRL
7260 KEY OFF:FOR LP=10 TO 1 STEP -1:KEY(LP)OFF:NEXT LP
7270 GOSUB 260:IF D(7)>=0 THEN 7310 ELSE SOUND 37,9
7280 GOSUB 10490:LOCATE 15,35:PRINT"SHIELD±&±ENERGY"
7290 LOCATE 20,49:PRINT"SHIELD CONTROL IS":LOCATE 21,49:PRINT"INOPERATIVE"
7300 GOSUB 210:GOTO 5740:RETURN
7310 IF C$<>"D" THEN 7400 ELSE BEEP
7320 LOCATE 14,31:PRINT"_ "+STRING$(42,95)+" _"
7330 LOCATE 15,30:PRINT"(_∫€€_SHIELD_&_ENERGY_"+STRING$(23,219)+"∫_)"
7340 LOCATE 17,30:PRINT L$;"Shield control is unnecessary."
7350 LOCATE 18,30:PRINT"The station's shields surround and protect the"
7360 LOCATE 19,30:PRINT"the Enterprise. Our shields are fully lowered,"
7370 LOCATE 20,30:PRINT"and our energy reserves have been completely"
7380 LOCATE 21,30:PRINT"restored.                               ----"
7390 GOSUB 210:GOTO 5740:RETURN
7400 GOSUB 9990 'S&E
7410 REM GRAPH
7420 LINE (248,175)-(352,175-SHD),1,BF:LINE (384,175)-(488,175-RES),1,BF
7430 LINE (520,175)-(623,175-WST),1,BF:TRUNC.S=S-(INT(S/200)*200)
7440 TRUNC.E=E-(INT(E/200)*200)
7450 SHD=FNPOW(S-TRUNC.S):RES=FNPOW(E-TRUNC.E):WST=ABS(INT(50-(RES+SHD)))
7460 REM
7470 LOCATE 23,34:PRINT" ":ANY$=INKEY$:LOCATE 23,34:PRINT CHR$(219)
7480 IF ANY$="" THEN 7470
7490 IF ANY$=CHR$(0)+"H" AND SHD<50 AND RES>0 THEN SHD=SHD+1:GOSUB 7550
7500 IF ANY$=CHR$(56) THEN IF SHD<=40 AND RES>=10 THEN SHD=SHD+10:GOSUB 7550 ELSE SHD=SHD+RES:GOSUB 7550
7510 IF ANY$=CHR$(50) THEN IF SHD>=10 THEN SHD=SHD-10:GOSUB 7550 ELSE SHD=0:GOSUB 7550
7520 IF ANY$=CHR$(0)+"P" AND SHD>0 THEN SHD=SHD-1:GOSUB 7550
7530 IF ANY$=CHR$(13) THEN 7620
7540 GOTO 7470
7550 REM
7560 LINE (352,125)-(248,125+(50-SHD)),0,BF
7570 LINE (248,175)-(352,175-SHD),1,BF
7580 RES=50-SHD-WST
7590 LINE (488,125)-(384,125+(50-RES)),0,BF
7600 LINE (384,175)-(488,175-RES),1,BF
7610 RETURN
7620 S=TRUNC.S+(SHD*200):E=TRUNC.E+(RES*200)
7630 GOSUB 260:GOTO 5740
7640 RETURN
7650 REM SCAN IN NAV
7660 GOSUB 7140:GOTO 6890
7670 REM REC IN NAV
7680 GOSUB 7220:GOTO 6890
7690 REM CHRT IN NAV
7700 GOSUB 7000:GOTO 6890
7710 REM 1ST INSTRUCT
7720 GOSUB 190:HP=10:CHAIN"003",,ALL
7730 RETURN
7740 REM SCAN IN MODES
7750 KEY(5)ON:ANY$="":GOSUB 7140:GOTO 5740
7760 REM REC IN MODES
7770 KEY(4)ON:GOSUB 7220:GOTO 5740
7780 REM CHART IN MODES
7790 KEY(6)ON:GOSUB 7000:GOTO 5740
7800 RETURN
7810 GOSUB 260:LOCATE 14,3:PRINT STRING$(26,32)
7820 REM TACT
7830 KEY OFF
7840 FOR LP=1 TO 10:KEY(LP)OFF:KEY LP,"±±±±±±":NEXT LP:CMD$="TACT"
7850 GOSUB 10120:GOSUB 7990
7860 KEY 1,"EXIT":ON KEY(1) GOSUB 8100:KEY(1)ON:KEY 2,"C.G.C":ON KEY(2)GOSUB 9560:KEY(2)ON
7870 IF D(5)>=0 THEN KEY 3,"PHOTON":ON KEY(3) GOSUB 8130
7880 IF D(4)>=0 THEN KEY 4,"PHASER":ON KEY(4) GOSUB 8300
7890 IF C$="D" THEN P=P0
7900 RES=FNPOW(E):SHE=FNPOW(S)
7910 LINE (504,168)-(504+2*RES,174),1,BF
7920 LINE (504,176)-(504+2*SHE,182),1,BF
7930 LOCATE 19,44:IF D(5)<0 THEN KEY(3)OFF:PRINT"DAMAGED " ELSE PRINT"DISARMED":IF P>0 THEN KEY(3)ON
7940 LOCATE 20,33:PRINT"____":LOCATE 20,35:PRINT USING"##";P
7950 LOCATE 18,61:IF D(4)<0 THEN KEY(4)OFF:PRINT"DAMAGED " ELSE PRINT"DISARMED":KEY(4)ON
7960 IF P<=0 THEN KEY(3)OFF:LOCATE 19,44:PRINT CHR$(17)+" NONE "+CHR$(16)
7970 KEY ON
7980 GOTO 6220
7990 LOCATE 16,31:PRINT SPACE$(22):LOCATE 16,31
8000 IF D(4)<0 AND D(5)<0 THEN PRINT" WEAPONS INOPERATIVE":RETURN
8010 IF C$="D" AND K3<=0 THEN PRINT" STATUS:    DOCKED"
8020 IF C$="D" AND K3>0  THEN PRINT"STATUS: *ALERT*/DOCKED"
8030 IF C$="R" AND S>=1000 THEN PRINT" * RED ALERT *"
8040 IF C$="R" AND S<1000 THEN PRINT" DANGER:  SHIELDS LOW!"
8050 IF C$="G" THEN PRINT" CONDITION:  GREEN"
8060 IF C$="Y" THEN PRINT " * YELLOW ALERT *"
8070 RETURN
8080 REM FN KEYS
8090 KEY(5)ON:GOSUB 10120:GOTO 7900:RETURN 'RESET
8100 GOTO 5740:RETURN 'EXIT
8110 GOTO 8130:RETURN 'PHOTON RESET
8120 GOTO 7820:RETURN 'PHOTON EXIT
8130 REM PHOTON TORPEDO CONTROL
8140 KEY OFF:FOR LP=1 TO 10:KEY LP,"±±±±±±":KEY (LP)OFF:NEXT LP:LOCATE 16,31:PRINT SPACE$(22):CMD$="TORP":IF C$="D" THEN P=P0
8150 KEY 1,"CANCEL":ON KEY(1)GOSUB 8120:KEY(1)ON
8160 KEY 2,"C.G.C.":ON KEY(2)GOSUB 9560:KEY(2)ON
8170 KEY 9," ? ? ?":ON KEY(9)GOSUB 1000:HP=5:KEY(9)ON:KEY ON
8180 REM P.T.CTRL
8190 LOCATE 19,44:PRINT"   ARMED":LOCATE 16,31:PRINT"ENTER TORPEDO COURSE "
8200 LOCATE 23,34:PRINT" ":ANY$=INKEY$:LOCATE 23,34:PRINT CHR$(176)
8210 IF ANY$="" THEN 8200 ELSE IF VAL(ANY$)<1 OR VAL(ANY$)>=9 THEN 8200
8220 LOCATE 23,34:PRINT ANY$
8230 LOCATE 23,36:PRINT" ":ANY$(1)=INKEY$:LOCATE 23,36:PRINT CHR$(176):IF ANY$(1)=CHR$(8) THEN LOCATE 23,36:PRINT" ":GOTO 8180
8240 IF ANY$(1)=CHR$(13) THEN ANY$(1)="0"
8250 IF ANY$(1)="" THEN 8230 ELSE IF ASC(ANY$(1))<48 OR ASC(ANY$(1))>57 THEN 8230 ELSE LOCATE 23,36:PRINT ANY$(1)
8260 C1=VAL(ANY$):C1(1)=VAL(ANY$(1)):LOCATE 16,31:PRINT"READY TO FIRE ON MARK"
8270 ANY$=INKEY$:IF ANY$="" THEN 8270 ELSE KEY OFF:FOR LP=1 TO 10:KEY(LP)OFF:NEXT LP:BEEP:SOUND 32767,.1:BEEP
8280 GOSUB 260:KEY OFF:LOCATE 19,30:PRINT W$;"Photon torpedo launched."
8290 TORP$=CHR$(196)+CHR$(236)+CHR$(196):GOTO 2940:RETURN
8300 REM PHAS CTRL
8310 KEY OFF:FOR LP=1 TO 10:KEY(LP)OFF:KEY LP,"±±±±±±":NEXT LP:PHA=0:X=0
8320 CMD$="PHA":KEY 1,"CANCEL":ON KEY(1)GOSUB 8370:KEY(1)ON
8330 KEY 9," ? ? ?":ON KEY(9) GOSUB 1000:HP=6:KEY(9)ON
8340 KEY 2,"C.G.C.":ON KEY(2)GOSUB 9560:KEY(2)ON
8350 KEY ON:GOTO 8380
8360 REM FN KEYS
8370 KEY OFF:GOTO 7820:RETURN 'EXIT
8380 LOCATE 18,61:PRINT"ƒ ƒARMED"
8390 TRUNC.E=E-(INT(E/200)*200):PHA=0
8400 SHE=FNPOW(S):RES=FNPOW(E-TRUNC.E)
8410 LINE (504,176)-(504+2*SHE,182),1,BF
8420 LOCATE 16,31:PRINT"POWER UP PHASERS (";CHR$(27)+CHR$(26);")"
8430 LOCATE 20,78:PRINT " "+CHR$(178):ANY$=INKEY$
8440 LOCATE 20,78:IF ANY$="" THEN PRINT CHR$(178)+" ":GOTO 8430
8450 LOCATE 20,78:PRINT STRING$(2,176)
8460 IF ANY$=CHR$(0)+"M" AND PHA<50 AND RES>0 THEN PHA=PHA+1:RES=RES-1:GOSUB 8500
8470 IF ANY$=CHR$(0)+"K" AND PHA>0 THEN PHA=PHA-1:RES=RES+1:GOSUB 8500
8480 IF ANY$=CHR$(13) THEN 8560
8490 GOTO 8430
8500 REM
8510 LINE (604,168)-(604-2*(50-RES),174),0,BF
8520 LINE (504,168)-(504+2*RES,174),1,BF
8530 LINE (604,152)-(604-2*(50-PHA),158),0,BF
8540 LINE (504,152)-(504+2*PHA,158),1,BF
8550 RETURN
8560 REM firing
8570 IF PHA=<0 THEN 8370
8580 KEY OFF:FOR LP=1 TO 10:KEY(LP)OFF:KEY LP,"":NEXT LP
8590 LOCATE 14,3:PRINT SPACE$(26):GOSUB 260
8600 IF K3<1 AND B3>0 THEN 8660 ELSE E=TRUNC.E+(RES*200):X=PHA*200
8610 IF K3<1 THEN 8720
8620 IF K3>1 THEN X$=" all ":LP$="s" ELSE X$=" ":LP$=""
8630 LOCATE 18,30:PRINT W$;"Firing phasers at";X$;"enemy target";LP$;"..."
8640 IF D(7)<0 THEN X=X*RND(1)
8650 GOSUB 210:GOTO 2780
8660 IF INT(RND(1)*10)>5 THEN FOR LP=1 TO 30:SOUND 2000,.5:SOUND 1950,1:NEXT LP:SOUND 1950,5:TORP$="PE":X=B4:Y=B5:GOTO 3310
8670 LOCATE 14,30:PRINT W$;"Sir, we would be firing on Starbase";
8680 PRINT INT(100*(Q2/Q1));"!"
8690 LOCATE 16,30:PRINT R$;"Delay that order, lieutenant."
8700 LOCATE 17,30:PRINT"Captain, there is no threat here."
8710 GOSUB 210:GOTO 5740
8720 LOCATE 14,30:PRINT W$;"No enemy targets in sight."
8730 LOCATE 15,30:PRINT"Sensors show no effects to the quadrant."
8740 LOCATE 17,30:PRINT R$;"Sir, we really should conserve our":LOCATE 18,30:PRINT"energy. It's pointless to be firing into space."
8750 GOTO 8710
8760 LOCATE 18,30:PRINT W$;"No targets in sight."
8770 GOTO 8710
8780 RETURN
8790 REM ENG & DAM CTRL
8800 GOSUB 260
8810 REM
8820 KEY OFF:FOR LP=1 TO 10:KEY(LP)OFF:KEY LP,"±±±±±±":NEXT LP:BOOST=500
8830 LOCATE 14,3:PRINT SPACE$(26):GOSUB 10240:GOSUB 9460
8840 KEY 9," ? ? ?":ON KEY(9)GOSUB 8950:KEY(9)ON
8850 KEY 1,"EXIT":ON KEY(1)GOSUB 8940:KEY(1)ON
8860 IF C$="D" THEN KEY 5,"REPAIR":ON KEY(5)GOSUB 9440:KEY(5)ON:GOTO 8900
8870 IF D(6)<0 THEN FOR LP=3 TO 4:KEY(LP)OFF:KEY LP,"±±±±±±":NEXT LP:GOTO 8900
8880 KEY 4,"DIVERT":ON KEY(4)GOSUB 8980:KEY(4)ON
8890 IF E>1.1*BOOST THEN KEY 3,"BOOST":ON KEY(3)GOSUB 8970:KEY(3)ON ELSE KEY(3)OFF
8900 IF C$="D" THEN LOCATE 14,3:PRINT"STARBASE TECHNICIANS READY":GOTO 8920
8910 LOCATE 14,3:IF D(6)<0 THEN PRINT"DAMAGE CONTROL INOPERATIVE" ELSE IF E<1.5*BOOST THEN PRINT"LOW ENERGY: CANNOT BOOST" ELSE PRINT"DAMAGE CONTROL CREW READY"
8920 KEY ON:GOTO 6220
8930 REM FN KEYS
8940 KEY OFF:GOSUB 260:GOTO 5740:RETURN 'EXIT
8950 REM HELP
8960 HP=7:GOTO 1000 'DAM HELP
8970 SYS=0:GOTO 8990 'BOOST
8980 SYS=1 'DIVERT
8990 KEY OFF:FOR LP=1 TO 10:KEY(LP)OFF:KEY LP,"±±±±±±":NEXT LP
9000 KEY 1,"CANCEL":ON KEY(1)GOSUB 9030:KEY(1)ON
9010 KEY ON:LOCATE 14,3:PRINT SPACE$(26):GOTO 9040
9020 REM FN KEY
9030 KEY OFF:GOTO 8810:RETURN 'CANCEL
9040 LOCATE 17-SYS,43:PRINT CHR$(17):S.B=1:I=1
9050 LOCATE 14,3:PRINT"SELECT SYSTEM (";CHR$(27);CHR$(18);CHR$(26);")"
9060 IF S.B>8 THEN S.B=1 ELSE IF S.B<1 THEN S.B=8
9070 ON S.B GOSUB 9080,9090,9100,9110,9120,9130,9140,9150:GOTO 9160
9080 LOCATE 20,46:PRINT CHR$(178):I=1:RETURN
9090 LOCATE 21,46:PRINT CHR$(178):I=2:RETURN
9100 LOCATE 22,46:PRINT CHR$(178):I=8:RETURN
9110 LOCATE 23,46:PRINT CHR$(178):I=4:RETURN
9120 LOCATE 20,42:PRINT CHR$(178):I=7:RETURN
9130 LOCATE 21,42:PRINT CHR$(178):I=3:RETURN
9140 LOCATE 22,42:PRINT CHR$(178):I=6:RETURN
9150 LOCATE 23,42:PRINT CHR$(178):I=5:RETURN
9160 ANY$=INKEY$:IF ANY$="" THEN 9160 ELSE IF S.B>4 THEN 9230
9170 IF ANY$=CHR$(0)+"H" AND S.B>1 THEN GOSUB 9350:S.B=S.B-1:GOTO 9060
9180 IF ANY$=CHR$(0)+"P" AND S.B<4 THEN GOSUB 9350:S.B=S.B+1:GOTO 9060
9190 IF ANY$=CHR$(0)+"K" THEN GOSUB 9350:S.B=S.B+4:GOTO 9060 ELSE IF ANY$=CHR$(13) THEN 9270
9200 GOTO 9160
9210 IF S.B>4 THEN LP=15+S.B:LOCATE LP,42:PRINT"_"
9220 ANY$=INKEY$:IF ANY$="" THEN 9220 ELSE IF S.B>8 THEN S.B=1:GOTO 9170
9230 IF ANY$=CHR$(0)+"H" AND S.B>5 THEN GOSUB 9350:S.B=S.B-1:GOTO 9060
9240 IF ANY$=CHR$(0)+"P" AND S.B<8 THEN GOSUB 9350:S.B=S.B+1:GOTO 9060
9250 IF ANY$=CHR$(0)+"M" THEN GOSUB 9350:S.B=S.B-4:GOTO 9060 ELSE IF ANY$=CHR$(13) THEN 9270
9260 GOTO 9220
9270 IF S.B>8 OR S.B<1 THEN 9060
9280 IF S.B<5 THEN LP=19+S.B:LOCATE LP,46:PRINT"_"
9290 IF S.B>4 THEN LP=15+S.B:LOCATE LP,42:PRINT"_"
9300 KEY(1)ON:IF SYS=1 THEN 9390 'diverting?
9310 REM BOOST
9320 KEY(1)OFF:KEY OFF:D(I)=D(I)+(INT(RND(1)*10)/10+BOOST/200):E=E-BOOST
9330 IF I=2 AND D(2)>=0 THEN GOSUB 4400
9340 GOTO 8810
9350 REM RESTORE
9360 IF S.B<5 THEN LP=19+S.B:LOCATE LP,46:PRINT"_"
9370 IF S.B>4 THEN LP=15+S.B:LOCATE LP,42:PRINT"_"
9380 RETURN
9390 REM DIVERT
9400 IF D(I)>=0 THEN KEY(1)OFF:KEY OFF:D(I)=D(I)-BOOST/200:E=E+INT(100*RND(1)+BOOST/2) ELSE KEY(1)ON:KEY 1,"CANCEL":GOTO 9060
9410 IF INT(E+S)>E0 THEN E=INT(E0-S)
9420 IF I=2 AND D(2)<0 THEN GOSUB 4400
9430 GOTO 8810
9440 REM DOCKED & REPAIRING
9450 GOSUB 3710:GOSUB 260:GOTO 8810:RETURN
9460 REM STATUS
9470 FOR S.B=1 TO 8
9480 ON S.B GOSUB 9080,9090,9100,9110,9120,9130,9140,9150
9490 IF D(I)>=0 THEN D$(I)="+" ELSE D$(I)="-"
9500 IF S.B>4 THEN LP=15+S.B:LOCATE LP,42:PRINT"_":LOCATE LP,40:PRINT D$(I)
9510 IF S.B<5 THEN LP=19+S.B:LOCATE LP,46:PRINT"_":LOCATE LP,48:PRINT D$(I)
9520 NEXT S.B
9530 LOCATE 19,61:PRINT USING"#####";INT(S):LOCATE 21,61:PRINT USING"#####";INT(E):LOCATE 23,61:PRINT USING"#####";INT(E0-(E+S))
9540 LOCATE 16,71:PRINT USING"#####.#";ST
9550 RETURN
9560 REM CMP FNs
9570 KEY OFF:FOR LP=1 TO 10:KEY LP,"±±±±±±":KEY(LP)OFF:NEXT LP
9580 KEY 1,"CANCEL":ON KEY(1)GOSUB 9630:KEY(1)ON:GOSUB 260:KEY ON
9590 GOSUB 930:IF Z3<>0 THEN 1720
9600 GOSUB 9640:GOSUB 4940
9610 IF CMD$="TORP" THEN GOSUB 10120:GOTO 8130
9620 IF CMD$="PHA" THEN GOSUB 10120:GOTO 8300
9630 GOSUB 260:GOTO 7820 'CANCEL
9640 GOSUB 260:ON KEY(1)GOSUB 9630:KEY(1)ON:KEY OFF
9650 RETURN
9660 REM NO ACTION/TIME PASSES
9670 IF K3>0 OR T0=ST THEN RETURN ELSE TIMER STOP:TIME(1)=TIME(1)+1
9680 FOR LP=1 TO 10:KEY(LP)OFF:NEXT LP:IF TIME(1)>INT(RND(1)*3) THEN 11270
9690 PLAY"O3D":ST=ST+.1:FOR LP=1 TO 10:KEY(LP)ON:NEXT LP
9700 GOSUB 10490:LOCATE 21,48:PRINT"NOW AT STARDATE ";USING"#####.#";ST
9710 TD=3:GOSUB 320:IF K9<=0 THEN 4290 ELSE IF C$<>"D" THEN GOSUB 2070
9720 FOR LP=1 TO 10:KEY(LP)ON:NEXT LP:IF E+S<9000 THEN E=E+1000 ELSE E=E0-S
9730 TIMER ON:GOTO 1720
9740 REM MAIN PANEL
9750 GOSUB 260
9760 LOCATE 13:PRINT"Õ—"+STRING$(26,205)+"À"+STRING$(50,205)+"—"
9770 LOCATE 14:PRINT"≥SHORT RANGE SENSOR SCAN 8 ∫⁄"+STRING$(32,196)+"¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø≥"
9780 LOCATE 15:PRINT" ∆ÕÕ—ÕÕ—ÕÕ—ÕÕ—ÕÕ—ÕÕ—ÕÕ—ÕÕ—ÕÕπ≥ (_∫€€_MAIN_COMMAND_PANEL_€€€∫_)|  _ __________ ≥≥"
9790 LOCATE 16:PRINT"1√ƒ":LOCATE 16,28:PRINT"ƒ∂√ ƒ ƒ ƒ ¬ƒ¬¬¬¬¬¬¬¬¬¬¬¬∑CONDITION:≥ (_∫_STARDATE_)≥≥"
9800 LOCATE 17:PRINT"2√ƒ":LOCATE 17,28:PRINT"ƒ∂≥USED   ";:X$="∆±±±±±±±±±±±±±◊±±±∂":PRINT X$;"DOCKED≥     00000.0   ≥≥"
9810 LOCATE 18:PRINT"3√ƒ":LOCATE 18,28:PRINT"ƒ∂≥SHIELDS";X$;"*RED* ≥  _ __________ ≥≥"
9820 LOCATE 19:PRINT"4√ƒ":LOCATE 19,28:PRINT"ƒ∂≥RESERVE";X$;"YELLOW≥ (_∫_DEADLINE_)≥≥"
9830 LOCATE 20:PRINT"5√ƒ":LOCATE 20,28:PRINT"ƒ∂≥TOTAL E";X$;"GREEN ≥     00000.0   ≥≥"
9840 LOCATE 21:PRINT"6√ƒ":LOCATE 21,28:PRINT"ƒ∂√ƒƒƒƒƒƒƒ≈ƒ¡¡¡¡¡¡¡¡¡¡¡¡–ƒƒƒ◊ƒƒƒƒƒƒ¡ƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¥≥"
9850 LOCATE 22:PRINT"7√ƒ":LOCATE 22,28:PRINT"ƒ∂≥REGION:|"+SPACE$(17)+"∫QUADR( , )≥SECTOR( , )≥≥"
9860 LOCATE 23:PRINT"8¿ƒ":LOCATE 23,28:PRINT"ƒ∂¿ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ–ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒŸ":RETURN
9870 REM NAVIGATION
9880 LOCATE 14,32:PRINT"_ "+STRING$(43,95)+" _"
9890 LOCATE 15,30:PRINT" (_∫€_NAVIGATION_"+STRING$(30,219)+"∫_)"
9900 LOCATE 16,30:PRINT"  _   __________   _   __________   _ __________  "
9910 LOCATE 17,30:PRINT" (_≥ ≥_COURSE___) (_≥ ≥_DISTANCE_) (_∫_STARDATE_) "
9920 LOCATE 18,30:PRINT"    ∆   4 3 2  µ  (_≥ ≥_FACTOR___) (_∫__00000.0_) "
9930 LOCATE 19,30:PRINT"    ∆  5--+--1 µ  (_≥ ≥_E.T.A.___) (_∫_DEADLINE_) "
9940 LOCATE 20,30:PRINT"    ‘   6 7 8  æ  (_≥ ≥_00000.0__) (_∫__00000.0_) "
9950 LOCATE 21,30:PRINT"  ________ _____ ’"+STRING$(31,205)+"∏"
9960 LOCATE 22,30:PRINT" (QUADRANT∫_ , _)≥"+SPACE$(31)+"≥"
9970 LOCATE 23,30:PRINT" (_SECTOR_∫_ , _)‘"+STRING$(31,205)+"æ"
9980 RETURN
9990 REM SHIELD & ENERGY
10000 GOSUB 260
10010 LOCATE 14,32:PRINT"_ "+STRING$(43,95)+" _"
10020 LOCATE 15,30:PRINT" (_∫€_SHIELD_&_ENERGY_CONTROL_€€€€€€€€€€€€€€€€€∫_)"
10030 LOCATE 16,30:PRINT" ÷";:X$="ƒƒƒƒƒƒƒƒƒƒƒƒƒ":PRINT X$;"“ƒƒ“";X$;"“ƒƒ“";X$;"∑"
10040 LOCATE 17,30:PRINT" ∫";:X$=SPACE$(13):PRINT X$;"∫∞∞∫";X$;"∫€€∫";X$;"∫"
10050 LOCATE 18,30:PRINT" ∫";X$;"∫∞∞∫";X$;"∫€€∫";X$;"∫"
10060 LOCATE 19,30:PRINT" ∫";X$;"ÃÕÕπ";X$;"ÃÕÕπ";X$;"∫"
10070 LOCATE 20,30:PRINT" ∫";X$;"∫≤≤∫";X$;"∫±±∫";X$;"∫"
10080 LOCATE 21,30:PRINT" ∫";X$;"∫≤≤∫";X$;"∫±±∫";X$;"∫"
10090 LOCATE 22,30:PRINT" »_   _________ ÕÕ _ ___________ ÕÕ _ ___________º"
10100 LOCATE 23,30:PRINT" (_≥ ≥_SHIELDS_)  (_∫_RESERVE___)  (_∫_CONSUMED__)"
10110 RETURN
10120 REM TACTICAL DISPLAY
10130 LOCATE 14,30:PRINT" (_∫€_TACTICAL_&_DEFENSIVE_SYSTEMS_€€€€€€€€€€€€∫_)"
10140 LOCATE 15,30:PRINT"⁄ƒƒƒƒƒ ƒƒƒ ƒƒƒ ƒƒƒ ƒƒƒƒø≠    _ ___________        "
10150 LOCATE 16,30:PRINT"∆"+SPACE$(22)+"µ≥   (_∫_PHASERS___)       "
10160 LOCATE 17,30:PRINT"¿ƒƒ ƒƒƒƒ ƒƒƒ ƒƒƒ ƒƒƒ ƒƒŸ≠"+SPACE$(25):X$=SPACE$(13)
10170 LOCATE 18,30:PRINT STRING$(24,196)+"≈ƒ ƒƒ ƒƒ ƒƒ ƒƒ ƒƒ ƒƒ ƒƒ ƒƒ"
10180 LOCATE 19,30:PRINT"(_∫_PHOTONS_)           √ƒƒƒƒƒƒƒƒ¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬ƒƒ"
10190 LOCATE 20,30:PRINT" _ _______ ";X$;"≥PHASERS ∆";X$;"µ∞∞"
10200 LOCATE 21,30:PRINT"(_∫COURSE_)ﬂ 4 3 2 ﬂﬁ   √ƒƒƒƒƒƒƒƒ≈≈≈≈≈≈≈≈≈≈≈≈≈≈¥∞∞"
10210 LOCATE 22,30:PRINT"          ›5  ˘+˘  1ﬁ   ≥RESERVE ∆";X$;"µ∞∞"
10220 LOCATE 23,30:PRINT" --"+CHR$(26)+" . "+CHR$(27)+"--›‹ 6 7 8 ‹ﬁ   |SHIELDS ∆";X$;"µ∞∞"
10230 RETURN
10240 REM DAMAGE CONTROL
10250 LOCATE 14,30:PRINT"(_∫_ENGINEERING_DAMAGE_CONTROL_€€€€€€€€€€€€€€€€∫_)"
10260 LOCATE 15,30:PRINT"  _ ________   _____________ ∫"+STRING$(20,177)
10270 LOCATE 16,30:PRINT" (_∫_DIVERT_) (_+0250_UNITS_)∫±STARDATE:±00000.0±±"
10280 LOCATE 17,30:PRINT" (_∫_BOOST__) (_-0500_UNITS_)∫±±±±±±±’ÕÕÕÕÕÕÕÕÕÕÕÕ"
10290 LOCATE 18,30:PRINT"ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±±±±±±≥_ _________ "
10300 LOCATE 19,30:PRINT" ________   _ ∫ _   ________ Ã±∞∞∞∞∞±(_∫_SHIELDS_)"
10310 LOCATE 20,30:PRINT"(SHIELDS";:X$="_≥-≥_)∫(_≥-≥":PRINT X$+"_WARP___)∫±±±±±±±≥_ _________ "
10320 LOCATE 21,30:PRINT"(L.R.SEN"+X$+"_S.R.SEN)Ã±∞∞∞∞∞±(_∫_RESERVE_)"
10330 LOCATE 22,30:PRINT"(DAMAGE_"+X$+"COMPUTER)∫±±±±±±±≥_ _________ "
10340 LOCATE 23,30:PRINT"(PHOTONS"+X$+"_PHASERS)Ã±∞∞∞∞∞±(_∫_CONSUMED)"
10350 RETURN
10360 REM C.G.C.
10370 LOCATE 14,30:PRINT"(_∫__COMPUTER_GENERATED_COURSE_€€€€€€€€€€€€€€€€∫_)"
10380 LOCATE 15,30:PRINT STRING$(36,177)+"≤≤4≤≤3≤≤2≤≤±±±"
10390 LOCATE 16,30:PRINT"±ÆTARGETØ±±±±±(_∫_COURSE___)±-->±±±≤≤∞∞∞∞∞∞∞∞∞≤≤±±"
10400 LOCATE 17,30:PRINT"±±±∞∞∞"+STRING$(29,177)+"≤5∞∞∞±.±∞∞∞1≤±±"
10410 LOCATE 18,30:PRINT"±±±O F±±±±"+STRING$(23,219)+"±±≤≤∞∞∞∞∞∞∞∞∞≤≤±±"
10420 LOCATE 19,30:PRINT"±±±∞∞∞±±±±€=-@€Æ----≤≤≤----Ø€*-]€±±±≤≤6≤≤7≤≤8≤≤±±±"
10430 LOCATE 20,30:PRINT"±±"+STRING$(39,219)+"±±±±±±±±±":X$=STRING$(15,177)
10440 LOCATE 21,30:PRINT"±±€(   ,   )€"+X$+"±±€(   ,   )€±±±±±±±±±"
10450 LOCATE 22,30:PRINT"±±€€€€€€€€€€€±(_∫_SHIELDS__)±±€€€€€€€€€€€±±±±±±±±±"
10460 LOCATE 23,30:PRINT X$+"€€€€€±±±%€€€"+STRING$(23,177)
10470 RETURN
10480 REM INFO PANEL
10490 GOSUB 260
10500 LOCATE 14,30:PRINT" _ "+STRING$(44,95)+" _ "
10510 LOCATE 15,30:PRINT"(_∫"+STRING$(44,177)+"∫_)"
10520 LOCATE 16,30:PRINT"⁄"+STRING$(48,196)+"ø"
10530 LOCATE 17,30:PRINT"≥"+STRING$(48,176)+"≥"
10540 LOCATE 18,30:PRINT"≥"+STRING$(48,176)+"≥"
10550 LOCATE 19,30:X$="≥∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞":PRINT X$+"⁄"+STRING$(31,196)+"Ÿ":X$=X$+"≥"
10560 LOCATE 20,30:PRINT X$+STRING$(23,32)+"⁄ƒƒƒƒƒƒƒø"
10570 LOCATE 21,30:PRINT X$+STRING$(23,32)+"≥€€€€€€€≥"
10580 LOCATE 22,30:PRINT X$+"     ⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ€€€€€€€≥"
10590 LOCATE 23,30:PRINT X$+"     ≥"+STRING$(25,219)+"≥"
10600 PUT (103,114),CM,OR:RETURN
10610 REM ROMULANS FIRING PHOTONS
10620 TIMER OFF:IF K3<=0 THEN 9660  ELSE H8=1
10630 LP1=INT(RND(1)*(K3-1)+1):IF K(LP1,3)>=100 THEN GOSUB 10780:GOTO 10680
10640 TIMER ON:IF K(1,3)<100 AND K(2,3)<100 AND K(3,3)<100 AND K3>0 THEN ON TIMER(45) GOSUB 10670:TIMER ON
10650 RETURN:GOTO 1590
10660 REM NO E & MOVING
10670 GOSUB 3600:GOTO 1720:RETURN
10680 REM CALC DIR
10690 K(LP1,3)=K(LP1,3)-100 'E 4 TOR
10700 GOSUB 5040 'DIR
10710 X=INT(RND(1)*10)'ODDS
10720 IF X>7 THEN DIR=DIR+(RND(0)*.1+.1)
10730 IF X<3 THEN DIR=DIR-(RND(0)*.1+.1)
10740 C1=DIR:TORP$=" "+CHR$(249)+" "
10750 SOUND 400,2:SOUND 300,1:SOUND 200,1:SOUND 100,1:SOUND 75,1:SOUND 50,1:SOUND 57,10
10760 LOCATE 15,30:PRINT"They're firing photon torpedoes...."
10770 LOCATE 17,30:PRINT R$;"Brace for impact!!!":GOTO 10820
10780 REM WARNING
10790 KEY OFF:FOR LP=1 TO 10:KEY(LP)OFF:NEXT LP:GOSUB 260
10800 GOSUB 260:LOCATE 14,30:PRINT W$;"Romulan at (";USING "#_,#_)";K(LP1,2),K(LP1,1);:PRINT" arming their weapons!"
10810   RETURN
10820 IF C1-INT(C1)>=.5 THEN C1=C1-1
10830 IF C1>=9 OR C1<=0 THEN BEEP:C1=INT(RND(1)*7+1)
10840 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1))
10850 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):X=K(LP1,1):Y=K(LP1,2)
10860 O.X=X1:O.Y=X2:S1.X=K(LP1,1):S2.X=K(LP1,2)
10870 H8=1:GOTO 3010 'PHO FIRE
10880 REM ERRORS
10890 SOUND 800,1:SOUND 32767,1:SOUND 800,8
10900 IF ERR<>14 THEN RESUME NEXT
10910 LOCATE 14,3:PRINT SPACE$(26):LOCATE 14,3:PRINT"LOW MEMORY:";FRE("a"):RESUME NEXT
10920 REM GRAPHICS
10930 REM WARP & STARS
10940 LINE (85,8)-(536,70),1,BF:GOSUB 300
10950 PLAY "MB ML":FOR LP=100 TO 101+INT((1/VEL*W1)*100):SOUND LP,.5:NEXT LP:SOUND LP,5000:TM=TIMER:TD=INT(W1+2/VEL)+1
10960 IF TIMER<TM+TD THEN GOSUB 11100:GOTO 10960 ELSE SOUND 32767,0
10970 FOR Z1=LP TO 37 STEP -1:SOUND Z1,.25:NEXT Z1
10980 LINE (85,8)-(536,70),1,BF:GOSUB 300
10990 GOSUB 300:FOR LP=1 TO INT(RND(1)*20+50)
11000 X1=INT(RND(1)*450+86):Y1=INT(RND(1)*62+8)
11010 PSET(X1,Y1)
11020 NEXT LP:RETURN
11030 REM *VWR GFX*
11040 LINE (16,32)-(23,55),1,BF:LINE (600,32)-(607,55),1,BF:IF K3<=0 THEN GOSUB 300
11050 DEF SEG=&HB800:BLOAD GFX$+".GX1",0:BLOAD GFX$+".GX2",8192:RETURN
11060 REM EXPLODE
11070 TIMER OFF:RANDOMIZE TIMER:IF X=4802 THEN Z=0 ELSE Z=1:X=B5:Y=B4:GOSUB 620
11080 GOSUB 11340:GOSUB 11400:FOR X=1 TO 5:LINE (85,8)-(536,70),INT(RND(1)*2),BF:IF INT(RND(0)*2)=1 THEN X=X+1:GOSUB 300
11090 NEXT X:SOUND 800,10:GOTO 10980
11100 REM STARS IN WARP
11110 X=INT(RND*20+300):Y=INT(RND*2+38)
11120 X2=INT(RND*451+85):Y2=INT(RND*2)*61+9
11130 IF (Y2-Y)=0 OR (X2-X)=0 THEN 11120
11140 X1=X:Y1=Y:M=(Y2-Y)/(X2-X):Y.INT=Y-M*X
11150 FOR LP3=X TO X2 STEP INT(RND*30 +20)*((X2-X)/ABS(X2-X))+VEL
11160 Y3=INT(M*LP3+Y.INT)
11170 LINE (X1,Y1)-(LP3,Y3),1:LINE (X1,Y1)-(LP3,Y3),0:X1=LP3:Y1=Y3:NEXT LP3
11180 RETURN
11190 FOR LP=1 TO W1*10:L1=RND*.05:L2=INT(RND*2):IF L2=1 THEN L2=-30:SD=-1 ELSE L2=30:SD=1
11200 X1=INT(RND*420+100):X=309:IF X1=309 THEN X1=310
11210 FOR W=0 TO L2 STEP SD
11220 X=X+(X1-309)/30
11230 L=L1*W:Y=39+W:Y1=39+(W-15*SD)*2
11240 IF ABS(W)<=30 THEN LINE(X-L,Y)-(X+L,Y),1
11250 IF ABS(W)>=15 THEN LINE(88,39)-(536,Y1),0,BF
11260 NEXT W,LP:RETURN
11270 REM TIME UP
11280 BEEP:GOSUB 5260:A$="*-]":K3=1:G(Q1,Q2)=G(Q1,Q2)+100:K9=K9+1:K7=K7+1
11290 K(1,1)=R1:K(1,2)=R2:K(1,3)=INT(S9*45+1000*RND(1)):K3=1:Z(Q1,Q2)=G(Q1,Q2)
11300 GOSUB 5280:X2$="X":GOSUB 1310:X2$="":GOSUB 4400:TIMER ON:TIME(1)=0
11310 FOR LP=10 TO 1 STEP -1:KEY(LP)ON:NEXT LP:GOTO 1720
11320 REM Break
11330 BEEP:KEY(15)ON:RETURN
11340 REM EXPLODE
11350 TM=TIMER:RANDOMIZE TIMER
11360 IF Z=1 THEN X=INT(RND*(X(2)-X(1))+X(1)):Y=INT(RND*30+25)
11370 IF Z<>1 THEN Y=INT(RND*15+32):X=INT(RND*266+240)
11380 CIRCLE(X,Y),INT(RND(1)*20+10):PAINT(X,Y),1:CIRCLE(X,Y),INT(RND(0)*20+10),0
11390 IF TIMER<TM+5 THEN 11360 ELSE RETURN
11400 SOUND 32767,0:FOR LP=1 TO 450:SOUND INT(RND(1)*500 +37),.1:NEXT LP:SOUND INT(RND(0)*500+37),30
11410 SOUND 800,1:SOUND 32767,1:SOUND 800,20:Z=0:RETURN
11420 REM RESCAN
11430 GOSUB 10490:FOR LP=1 TO 10:KEY(LP)OFF:NEXT LP:KEY OFF
11440 LOCATE 15,35:PRINT"SHORT±RANGE±SENSOR±SCAN"
11450 LOCATE 14,3:PRINT"SHORT RANGE SENSOR SCAN "+CHR$(25)+" ":LOCATE 21,50:IF D(2)>=0 THEN PRINT"RESCANNING QUADRANT" ELSE PRINT"SENSORS DAMAGED"
11460 GOSUB 130:GOSUB 4310:KEY(5)ON:TIMER ON:GOTO 1720
