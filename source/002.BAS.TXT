10 '002.BAS/SETUP.BAS -- LAST MODIFIED: 9-6-95
20 SCREEN 2,1:KEY OFF:DEF SEG=&HB800:BLOAD"00K.GFX",0
30 KEY 15,CHR$(4)+CHR$(70):ON KEY(15)GOSUB 360:KEY(15)ON
40 LOCATE 23,40:PRINT"LOADING PROGRAM INTO MEMORY..."
50 CLEAR,,1500:RANDOMIZE TIMER
60 DIM G(8,8),C(9,2),K(3,3),N(3),Z(8,8),D(8)
70 Z$=SPACE$(25)
80 R$="CMDR.RIKER: ":D$="LT.CMDR.DATA: ":W$="LT.WORF: ":WC$="ENS.CRUSHER: ":L$="LT.CMDR.LaFORGE: "
90 ST=48020!+(INT(RND(1)*25+25))/10:T0=ST:T9=25+INT(RND(1)*10):D0=0:E=10000:E0=E
100 P=10:P0=P:S9=200:S=0:B9=0:K9=0
110 DEF FND(D)=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2) 'DISTANCE FORMULA
120 DEF FNR(R)=INT(RND(R)*7.98+1.01) 'plot random location in grid
130 REM INITIALIZE ENTERPRISE's POSITION
140 Q1=FNR(1):Q2=FNR(1):S1=FNR(1):S2=FNR(1)  'setting ENTERP's Quad & Sector
150 FOR I=1 TO 9:C(I,1)=0:C(I,2)=0:NEXT I 'COURSE
160 '
170 C(3,1)=-1:C(2,1)=-1:C(4,1)=-1:C(4,2)=-1:C(5,2)=-1:C(6,2)=-1
180 C(1,2)=1:C(2,2)=1:C(6,1)=1:C(7,1)=1:C(8,1)=1:C(8,2)=1:C(9,2)=1
190 '
200 FOR I=1 TO 8:D(I)=0:NEXT I
210 REM ** SET UP WHAT EXISTS IN THE GALAXY **
220 'K3= # ENEMY (FORMERLY KLINGONS)| B3= # STARBASES | S3= # STARS
230 '                           ***** # OF ENEMY SHIPS ******
240 FOR I=1 TO 8:FOR J=1 TO 8:K3=0:Z(I,J)=0:R1=RND(1)
250 IF R1>.98 THEN K3=3:K9=K9+3:GOTO 280
260 IF R1>.95 THEN K3=2:K9=K9+2:GOTO 280
270 IF R1>.8 THEN K3=1:K9=K9+1
280 B3=0:IF RND(1)>.96 THEN B3=1:B9=B9+1
290 G(I,J)=K3*100+B3*10+FNR(1):NEXT J:NEXT I:IF K9>T9/2 THEN T9=K9*2 +INT(RND(1)*10)
300 IF B9<>0 THEN 330
310 IF G(Q1,Q2)<200 THEN G(Q1,Q2)=G(Q1,Q2)+100:K9=K9+1
320 B9=1:G(Q1,Q2)=G(Q1,Q2)+10:Q1=FNR(1):Q2=FNR(1)
330 K7=K9:IF B9<>1 THEN X$="S":X0$=" ARE "
340 X3$="NEW":DL=ST+T9:LOCATE 23,30:PRINT SPACE$(50)
350 CHAIN "001",,ALL
360 BEEP:KEY(15)ON:RETURN
