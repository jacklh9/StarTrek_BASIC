10 '004.BAS/SCORES -- LAST MODIFIED: 10-20-95
20 SCREEN 2,1:CLS:ON ERROR GOTO 990:KEY OFF
30 KEY 15,CHR$(4)+CHR$(70):ON KEY(15)GOSUB 740:KEY(15)ON
40 IF LP=4802 THEN 260 '<--- BACK FROM CREDITS
50 IF LP=104 THEN 150 '<--- winner to credits
60 ERASE ANY$:DIM SCORE(10):DIM ANY$(48):DEF SEG=&HB800
70 IF ST=0 THEN 760 ELSE IF K7-K9=0 THEN 760
80    REM END OF GAME
90 TIMER OFF
100 BEEP:PRINT"IT IS STARDATE";ST
110 PRINT"THERE WERE";K9;"OF";K7;"ROMULAN WARBIRDS LEFT AT"
120 PRINT"THE END OF YOUR MISSION TIME."
130 GOTO 170  '<--- SCORE BOARD
140 CLEAR:GOTO 260
150 TIMER OFF:PLAY"CG":CLS:PRINT"CONGRATULATIONS, CAPTAIN! THE LAST KNOWN WARBIRD IN YOUR PART OF THE GALAXY"
160 PRINT"MENACING THE FEDERATION HAS BEEN DESTROYED.":PRINT"THIS WILL BE A GREAT ASSET TO THE WAR EFFORT.":LP=4802
170             'POINTS --------
180 IF ST-T0=0 THEN ST=ST+1
190 RATING=INT(10000*(K7-K9)/(DL-T0)+(ST-T0))
200 IF RATING<=0 THEN RATING=0
210 IF RATING>9999 THEN RATING=9999
220 '-----
230 LOCATE 15,10:PRINT"** PRESS <RETURN> or <SPACE BAR> TO CONTINUE **"
240 ANY$=INKEY$:IF ANY$=CHR$(13) OR ANY$=" " THEN LOCATE 15:PRINT STRING$(80,32):BEEP:PRINT"WORKING...PLEASE WAIT!" ELSE 240
250 IF LP=4802 THEN CHAIN"005",,ALL ELSE 260
260 '---
270      IF RATING>9999 THEN RATING=9999
280 OPEN"R",1,"006.DAT",54:OPEN "R",2,"006.DAT",54
290 FIELD 1,24 AS N$,4 AS S$,24 AS C$,2 AS MARK$
300 FOR LP%=9 TO 1 STEP -1:GET 1,LP%:IF MARK$<>"**" THEN 450
310              'COMPARE SCORES
320 IF RATING>VAL(S$) THEN 450
330 NEXT LP%
340 GOTO 760 'LOSING SCORE
350 '
360 REM ** INSERT SLOT & REORDER NAMES
370 FIELD 1,54 AS DAT$(1):FIELD 2,54 AS DAT$(2)
380 FOR LP1%=1 TO LP%
390     IF LP1%=1 THEN LSET DAT$(1)=STRING$(54,32):PUT 1,LP1%:GOTO 430
400     GET 1,LP1%:TRANS$=DAT$(1)
410     LSET DAT$(1)=STRING$(54,32):PUT 1,LP1%
420     LSET DAT$(2)=TRANS$:PUT 2,LP1%-1
430 NEXT LP1%:CLOSE:OPEN "R",1,"006.DAT",54
440 RETURN
450 'TOP SCORER
460 IF K7=K9 THEN 760
470 GOSUB 360
480 FIELD 1,24 AS N$,4 AS S$,24 AS C$,2 AS MARK$
490 FIELD 1,54 AS INFO$
500 PLAY "T255 Mb Ml O3 C2G4<>B-1MLF4P32D8<B-7D8>C2P16C8E1.>"
510 LN=INT((10-LP%)*2+3)
520 GOSUB 620 '<---- GET NAME
530 IF NAM$=SPACE$(LEN(NAM$)) THEN GOSUB 640:GOTO 530
540 NEW.NAM$=NAM$+SPACE$(24-LEN(NAM$))
550 NEW.DAT$=DATE$:RATING=INT(RATING):NEW.SCORE$=RIGHT$(STR$(RATING),4):NEW.SCORE$=SPACE$(4-(LEN(NEW.SCORE$)))+NEW.SCORE$:MARK$="**":K7=INT(K7):K9=INT(K9)
560 NEW.ROM$=RIGHT$(STR$(K7-K9),2):NEW.ROM$=NEW.ROM$+SPACE$(2-(LEN(NEW.ROM$)))
570 IF STATUS$="" THEN ELSE NEW.STATUS$=STATUS$:GOTO 590
580 IF K9<=0 THEN NEW.STATUS$="COMPLETED" ELSE IF ST>DL THEN NEW.STATUS$="OUTofTIME" ELSE IF S<0 AND E>0 THEN NEW.STATUS$="PERISHED±" ELSE IF E+S<=0 THEN NEW.STATUS$="OUTofFUEL" ELSE NEW.STATUS$="±ABORTED±"
590 LSET INFO$=NEW.NAM$+NEW.SCORE$+NEW.STATUS$+NEW.DAT$+NEW.ROM$+"/"+RIGHT$(STR$(K7),2)+MARK$
600 PUT 1,LP%:CLOSE
610 GOTO 780
620 REM ****** WINNER'S NAME
630 X=24:DEF SEG=&HB800:BLOAD"00L.GFX",0
640 LOCATE LN,X:PRINT CHR$(219):ANY$(X)=INKEY$:IF ANY$(X)="" THEN LOCATE LN,X:PRINT" ":GOTO 640 ELSE SOUND 5000,1:LOCATE LN,X:PRINT CHR$(177)
650           IF ANY$(X)=CHR$(13) THEN 720 ELSE IF ANY$(X)=CHR$(0)+"K" THEN 710
660           IF ANY$(X)=CHR$(27) OR ANY$(X)=CHR$(8) THEN 710
670           IF ANY$(X)=CHR$(9) OR LEFT$(ANY$(X),1)=CHR$(0) THEN 640
680 IF X>=47 THEN LOCATE LN,47:PRINT ANY$(47):X=48:ANY$(X)=INKEY$:IF ANY$(X)="" THEN 680 ELSE 650
690 LOCATE LN,X:PRINT ANY$(X)
700 X=X+1:GOTO 640
710 ANY$(X)="":IF X>24 THEN X=X-1:ANY$(X)="":LOCATE LN,X:PRINT CHR$(177):GOTO 640 ELSE ANY$(25)="":ANY$(24)="":X=24:GOTO 640
720 ANY$(X)="":NEW.NAM$="":FOR LP=1 TO 24:NAM$=NAM$+ANY$(LP+23):NEXT LP
730 BEEP
740 RETURN
750 '---------------------
760 CLOSE
770 DEF SEG=&HB800:BLOAD"00L.GFX"
780 OPEN "R",1,"006.DAT",54
790 FIELD 1,24 AS NAM$,4 AS SCORE$,9 AS STATUS$,10 AS DAT$,5 AS ROM$,2 AS MARK$
800 FOR LP%=9 TO 1 STEP -1
810     GET 1,LP%: IF MARK$="**" THEN GOSUB 890
820 NEXT LP%
830 SOUND 1000,1
840 CLOSE:CLEAR
850 FOR LP=1 TO 10:KEY LP,"":KEY(LP)OFF:NEXT LP
860 ON KEY(1)GOSUB 880:ON KEY(10)GOSUB 950:KEY(1)ON:KEY(10)ON
870 GOTO 1020
880 PLAY "CG":RUN"002.BAS" '--- NEW MISSION
890 '------ FILE OPEN
900 LN=INT((10-LP%)*2+3)
910 LOCATE LN,24:PRINT NAM$
920 LOCATE LN,49:PRINT STATUS$
930 LOCATE LN,59:PRINT DAT$:LOCATE LN,70:PRINT SCORE$:LOCATE LN,75:PRINT ROM$
940 RETURN
950 '-- QUIT
960 LOCATE 23,24:PRINT"LIVE LONG AND PROSPER, CAPTAIN."+CHR$(186)+STRING$(24,177):TM=TIMER:BEEP
970 IF TM+5>=TIMER THEN 970
980 BEEP:CLS:CHAIN"008.BAS"
990 'ERROR
1000 BEEP:BEEP:RESUME NEXT
1010 BEEP:KEY(15)ON:RETURN
1020 '------ GFX ANIMATION STARTS HERE
1030 DEFINT A,C,E,G,L,M,P:DIM C(100),E(100),P(50)
1040 GET (73,7)-(126,9),C:GET (73,184)-(126,186),E:GET (0,83)-(33,89),P
1050 PUT (73,184),C,PSET:GOTO 1300
1060 '---- ENERGY
1070 M.Y(N)=-37+22*N+((N1*4)-4):RETURN '<----- MATTER ENERGY
1080 A.Y(N)=206-22*N-((N1*4)-4):RETURN '<- ANTIMATTER ENERGY
1090 '---- FLOW
1100 C1=N1-1:IF C1<=0 THEN C1=4
1110 CM.Y(N)=-15+22*N+((C1*4)-4):RETURN '<---- MATTER ENERGY
1120 C1=N1-1:IF C1<=0 THEN C1=4
1130 CA.Y(N)=206-22*N-((C1*4)-4):RETURN '< ANTIMATTER ENERGY
1140 '----
1150 BEEP:PUT(73,106),C,PSET:PUT (73,63),C,PSET:RETURN'<- CLEAR LAST PIPES
1160 FOR N1=1 TO 4:FOR N=1 TO 4
1170    GOSUB 1070:GOSUB 1100           '<---- MATTER FLOW
1180    GOSUB 1080:GOSUB 1120           '< ANTIMATTER FLOW
1190            NEXT N:FOR N=LP TO LP1
1200 IF N<>1 THEN PUT (73,M.Y(N)),E,PSET
1210              PUT (73,A.Y(N)),E,PSET
1220           NEXT N:FOR N=1 TO 4
1230 IF N<>4 THEN PUT (73,CM.Y(N)),C,PSET
1240              PUT (73,CA.Y(N)),C,PSET
1250 'IF N=1 THEN IF N1=LP1 AND N1=4 THEN GOSUB 1330
1260              IF N1=4 AND LP1=4 THEN LINE (0,85)-(33,86),1,BF ELSE 1280
1270              LINE (0,83)-(33,89),0,BF:PUT (0,83),P,PSET
1280       NEXT N:GOSUB 1330
1290 NEXT N1:RETURN
1300 '--- PROGRAMMED REACTION
1310 LP=1:FOR LP1=1 TO 4:GOSUB 1160:NEXT LP1:LP1=4:GOSUB 1160
1320 FOR LP=1 TO 4:GOSUB 1160:NEXT LP:GOTO 1310
1330 '--- CHAMBER REACTION
1340 FOR LP2=6 TO 1 STEP -1:FOR LP3=1 TO 0 STEP -1
1350    LINE (87+LP2*7,85)-(92+LP2*7,86),LP3,BF
1360 NEXT LP3,LP2
1370 RETURN
1380 '--- CONDUIT REACTION
1390 FOR CN.R=1 TO 4:LINE (0,85)-(33,86),1,BF
1400 LINE (0,83)-(33,89),0,BF:PUT (0,83),P,PSET:NEXT CN.R
1410 RETURN
